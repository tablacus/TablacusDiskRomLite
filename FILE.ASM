;
;	ディスクからメモリへの転送
;
ROM_LDIR:
	LD	A,(FLG_LDIR)
	OR	A
	JR	NZ,ROM_LDIRVM
	BIT	7,D
	JP	Z,ROM_LDIRSLT
;
;	ページ2/ページ3
;
#if exists USE_NORMAL32K_OR_ASCII16
	DI
	LD	(_SP),SP
	LD	A,(_SP_H)
	CP	0C0H
	JR	NC,SPJ1
	LD	SP,SPBUF
SPJ1:
LDIR_PAGE2_1:
	LD	A,B
	OR	C
	JR	Z,LDIRE

	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(ROM_SLT)
	LD	H,080H
	CALL	_ENASLT
	POP	HL
	POP	DE
	POP	BC
LDIR_PAGE2_2:
	BIT	6,D
	JR	NZ,LDIR_PAGE2_4

	PUSH	BC
	PUSH	DE
	LD	DE,BUF

	LD	A,B
	OR	A
	JR	Z,LDIR_PAGE2_3
	LD	BC,00100H		;B != 0 の場合は256バイトずつ転送
LDIR_PAGE2_3:
	PUSH	BC
	LDIR
	LD	(_HL),HL

	LD	A,(RAMAD2)
	LD	H,080H
	CALL	_ENASLT

	POP	BC
	POP	DE
	LD	HL,BUF
	LDIR

	LD	HL,(_HL)
	POP	BC
	LD	A,B
	OR	A
	JR	Z,LDIRE
	DEC	B
	JR	LDIR_PAGE2_1
LDIR_PAGE2_4:				;ページ3
#endif
	LDIR

#if exists USE_NORMAL32K_OR_ASCII16
LDIRE2:
	PUSH	DE
	PUSH	HL
	LD	A,(RAMAD2)
	LD	H,080H
	CALL	_ENASLT
	POP	HL
	POP	DE
LDIRE:
	LD	SP,(_SP)
#else
LDIRE2:
#endif
	RET
;
;	ディスクからV-RAMへの転送
;
ROM_LDIRVM:
#if exists USE_NORMAL32K_OR_ASCII16
	DI
	LD	(_SP),SP
	LD	A,(_SP_H)
	CP	0C0H
	JR	NC,SPJ2
	LD	SP,SPBUF
SPJ2:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(ROM_SLT)
	LD	H,080H
	CALL	_ENASLT
	POP	HL
	POP	DE
	POP	BC
#endif
	PUSH	BC
	PUSH	DE
	LD	IX,LDIRVM
	CALL	CALSLT_BIOS_PAGE0
	POP	HL
	POP	BC
	ADD	HL,BC
	EX	DE,HL
#if exists USE_NORMAL32K_OR_ASCII16
	JR	LDIRE2
#else
	RET
#endif
;
;	ページ0/ページ1
;
ROM_LDIRSLT:
#if exists USE_NORMAL32K_OR_ASCII16
	DI
	LD	(_SP),SP
	LD	A,(_SP_H)
	CP	0C0H
	JR	NC,SPJ3
	LD	SP,SPBUF
SPJ3:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	A,(ROM_SLT)
	LD	H,080H
	CALL	_ENASLT
	POP	HL
	POP	DE
	POP	BC
#endif
				;ページ0
	LD	A,(0000H)
	CP	0F3H		;0000H が DI じゃない場合はページ0は RAM とみなす
	JR	Z,LDIR_PAGE0_SLT
LDIR_PAGE0_1:
	BIT	6,D
	JR	NZ,LDIR_PAGE1
	LDI			;ページ0 直接転送
	JP	PE,LDIR_PAGE0_1
#if exists USE_NORMAL32K_OR_ASCII16
	JP	LDIRE2
#else
	RET
#endif

LDIR_PAGE0_SLT:		;ページ0 WRSLTを使用
	EX	DE,HL		;転送方向(DE)→(HL)
LDIR_PAGE0_SLT1:
	BIT	6,H
	JR	NZ,LDIR_PAGE1_DEHL
	LD	A,(DE)
	INC	DE
	PUSH	DE
	LD	E,A
	PUSH	HL
	PUSH	BC
	LD	A,(RAMAD0)
	CALL	_WRSLT
	POP	BC
	POP	HL
	POP	DE
	CPI			;HL=HL+1 BC=BC-1 BCが0の場合にP/V=0
	JP	PE,LDIR_PAGE0_SLT1
	EX	DE,HL		;転送方向(HL)→(DE)
#if exists USE_NORMAL32K_OR_ASCII16
	JP	LDIRE2
#else
	RET
#endif

#if exists _RAM64K
LDIR_PAGE1_DEHL:
	EX	DE,HL		;転送方向(HL)→(DE)
LDIR_PAGE1:			;ページ1
	LD	A,B
	OR	C
	JP	Z,LDIRE2

	LD	A,D
	CP	07FH		;ページ2と被る可能性？($7FXX)
	JR	C,LDIR_PAGE1_2
	ADD	A,A
	JP	C,LDIR_PAGE2_1	;ページ2へ
	LD	A,B
	OR	A
	LD	A,E
	JR	Z,LDIR_PAGE1_1
	OR	A			;B != 0 の場合は256バイト転送
	JR	NZ,LDIR_PAGE1_SLT_HLDE	;被る
LDIR_PAGE1_1:
	ADD	A,C
	JR	C,LDIR_PAGE1_SLT_HLDE	;被る
LDIR_PAGE1_2:
	PUSH	BC
	PUSH	DE
	LD	DE,BUF

	LD	A,B
	OR	A
	JR	Z,LDIR_PAGE1_3
	LD	BC,00100H		;B != 0 の場合は256バイトずつ転送
LDIR_PAGE1_3:
	PUSH	BC
	LDIR
	LD	(_HL),HL

	LD	A,(RAMAD1)
	JP	LDIR_PAGE1_RAM		;ページ1をRAMにする処理はページ3にある
LDIR_PAGE1_ROM:
	LD	HL,(_HL)
	POP	BC
	LD	A,B
	OR	A
	JP	Z,LDIRE2
	DEC	B
	JR	LDIR_PAGE1

LDIR_PAGE1_SLT_HLDE:
	EX	DE,HL		;転送方向(DE)→(HL)
#else
LDIR_PAGE1:			;ページ1 WRSLTを使用
LDIR_PAGE1_HLDE:
	EX	DE,HL		;転送方向(DE)→(HL)
LDIR_PAGE1_DEHL:
#endif
LDIR_PAGE1_SLT1:
	BIT	7,H
	JR	NZ,LDIR_PAGE2_HLDE
	LD	A,(DE)
	INC	DE
	PUSH	DE
	LD	E,A
	PUSH	HL
	PUSH	BC
	LD	A,(RAMAD1)
	CALL	_WRSLT
	POP	BC
	POP	HL
	POP	DE
	CPI			;HL=HL+1 BC=BC-1 BCが0の場合にP/V=0
	JP	PE,LDIR_PAGE1_SLT1
	EX	DE,HL		;転送方向(HL)→(DE)
#if exists USE_NORMAL32K_OR_ASCII16
	JP	LDIRE2
LDIR_PAGE2_HLDE:			;ページ2
	EX	DE,HL		;転送方向(HL)→(DE)
	JP	LDIR_PAGE2_1
#else
	RET
LDIR_PAGE2_HLDE:			;ページ2
	EX	DE,HL		;転送方向(HL)→(DE)
LDIR_PAGE2_1:
	LDIR
	RET
#endif
;
;	BASIC関連
;
END_OF_LINE:
	LD	HL,BUF
EOL1:
	LD	A,(HL)
	RET	Z
	CP	00DH
	JR	Z,EOLE
	CP	00AH
	JR	Z,EOLE
	INC	HL
	JR	EOL1
EOLE:
	LD	(HL),0
	INC	HL
	LD	A,(HL)
	CP	00AH
	RET	NZ
	INC	HL
	RET
;
;	アスキー形式のファイルを読み込む
;
ROM_LOAD_ASCII:
	LD	HL,0
	LD	(_BUF_ST),HL	;読み出し位置
	LD	HL,(TXTTAB)
	LD	(_BUF_EN),HL	;書き出し位置

	LD	HL,BUF
	LD	(_DTA),HL
RLOAD_A1:
	LD	HL,(_BUF_ST)
	LD	(RR_LOW),HL
	LD	HL,258
	CALL	ROM_READ
	LD	A,H
	OR	L
	JR	Z,RLOAD_AEND
	LD	BC,BUF
	ADD	HL,BC
	LD	(HL),0
	CALL	END_OF_LINE
	LD	BC,BUF
	AND	A
	SBC	HL,BC
	JR	Z,RLOAD_A2
	LD	C,L
	LD	B,H
	LD	DE,(_BUF_ST)
	ADD	HL,DE
	LD	(_BUF_ST),HL
	LD	A,(BUF)
	OR	A
	JR	Z,RLOAD_A1
RLOAD_A2:
	LD	DE,BUF
	CALL	SPCUTEX
	LD	A,(DE)
	OR	A
	JR	Z,RLOAD_AEND
	CALL	GETNUM
	LD	A,H
	OR	L
	JP	Z,ERROR_DIRECT_IN_FILE
	LD	IX,(_BUF_EN)
	LD	(IX+2),L
	LD	(IX+3),H

	CALL	SPCUT
	EX	DE,HL
	PUSH	IX
	LD	IX,CRUNCH
	CALL	CALSLT_BIOS
	POP	IX
	EX	DE,HL
	LD	DE,KBUF
	SCF
	SBC	HL,DE
	JP	Z,ERROR_DIRECT_IN_FILE
	JP	C,ERROR_DIRECT_IN_FILE
	LD	C,L
	LD	B,H
	LD	HL,(_BUF_EN)
	LD	DE,4
	ADD	HL,DE
	LD	DE,KBUF

	EX	DE,HL
	LDIR
	EX	DE,HL

	LD	(IX+0),L
	LD	(IX+1),H
	LD	(_BUF_EN),HL
	JP	RLOAD_A1

RLOAD_AEND:
	LD	HL,(_BUF_EN)
	XOR	A
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(_BUF_EN),HL
	JP	RLOAD_END1

ROM_LOAD:
	CALL	INIT_PARAM
	LD	A,(HL)
	CP	','
	JR	NZ,ROM_LOAD0
	INC	HL
	LD	A,(HL)
	DEC	HL
ROM_LOAD0:
	LD	(RUNBNF),A
	CALL	FILE_B
	LD	A,(FNAME)
	CP	020H
	JP	Z,ROM_ORG

	CALL	ROM_OPEN
	JP	C,ERROR_FILE_NOT_FOUND
ROM_LOAD1:
	LD	HL,_BUF
	LD	(_DTA),HL
	LD	HL,1			;ヘッダを1バイト読み込む
	CALL	ROM_READ
	LD	A,(_BUF)
	INC	A
	JP	NZ,ROM_LOAD_ASCII
	LD	HL,(TXTTAB)
	LD	(_DTA),HL
	EX	DE,HL
	LD	HL,-256
	ADD	HL,SP
	SBC	HL,DE
	CALL	ROM_READ
	LD	DE,(_DTA)
	ADD	HL,DE
	PUSH	HL
	LD	HL,(TXTTAB)
ROM_LOAD2:			;リンクポインタをFIX
	PUSH	HL
	POP	IX
	LD	A,(HL)		;リンクポインタL
	INC	HL
	OR	(HL)		;リンクポインタH
	INC	HL
	JR	Z,RLOAD_END0
	INC	HL		;行番号
	INC	HL		;行番号
ROM_LOAD3:
	LD	A,(HL)
	INC	HL
	CP	00BH		;8進数(&O)
	JR	Z,INC_HL2
	CP	00CH		;16進数(&H)
	JR	Z,INC_HL2
	CP	00DH		;行番号(RUN後)
	JR	Z,INC_HL2
	CP	00EH		;行番号(RUN前)
	JR	Z,INC_HL2
	CP	00FH		;10～255の整数(%)
	JR	Z,INC_HL1
	CP	01CH		;256～65535の整数(%)
	JR	Z,INC_HL2
	CP	01DH		;単精度実数(!)
	JR	Z,INC_HL4
	CP	01FH		;倍制度実数(#)
	JR	NZ,INC_HL0
	INC	HL
	INC	HL
	INC	HL
	INC	HL
INC_HL4:
	INC	HL
	INC	HL
INC_HL2:
	INC	HL
INC_HL1:
	INC	HL
INC_HL0:
	OR	A
	JR	NZ,ROM_LOAD3
	LD	(IX+0),L
	LD	(IX+1),H
	JR	ROM_LOAD2
RLOAD_END0:
	POP	HL
RLOAD_END1:
	LD	(VARTAB),HL
	LD	(ARYTAB),HL
	LD	(STREND),HL

	LD	A,(RUNBNF)
	CALL	CAP
	CP	'R'
	JR	Z,RLOAD_END2
	XOR	A
	LD	HL,_BUF+3
	LD	(HL),A		;3
	DEC	HL
	LD	(HL),A		;2
	DEC	HL
	LD	(HL),A		;1
	DEC	HL
	LD	A,08FH		;REM
	LD	(HL),A		;0
	RET
RLOAD_END2:
	PUSH	DE
	LD	DE,(TXTTAB)
	DEC	DE
	XOR	A
	LD	HL,_BUF+6
	LD	(HL),A		;6
	DEC	HL
	LD	(HL),A		;5
	DEC	HL
	LD	(HL),A		;4
	DEC	HL
	LD	(HL),D		;3 行番号H
	DEC	HL
	LD	(HL),E		;2 行番号L
	DEC	HL
	LD	(HL),00DH	;1 行番号(RUN後)
	DEC	HL
	LD	A,089H		;GOTO
	LD	(HL),A		;0
	POP	DE
	RET

ERROR_DIRECT_IN_FILE:
	LD	E,57 			;Direct statement in file
	DB	1			;LD	BC,
ERROR_DEVICE_IO_ERROR:
	LD	E,19 			;Device I/O error
	DB	1			;LD	BC,
ERROR_INTERNAL_ERROR:
	LD	E,51 			;Internal error
	DB	1			;LD	BC,
ERROR_FILE_NOT_OPEN:
	LD	E,59			;File not OPEN
	DB	1			;LD	BC,
ERROR_FILE_NOT_FOUND:
	LD	E,53			;File not found
ERROR:
	LD	IX,ERRHAND
	JP	CALSLT_BIOS

ROM_BLOAD:
	CALL	INIT_PARAM
	CALL	FILE_B
	CALL	ROM_OPEN
	JR	C,ERROR_FILE_NOT_FOUND

	LD	HL,_BUF
	LD	(_DTA),HL
	LD	HL,7			;ヘッダを7バイト読み込む
	CALL	ROM_READ
	LD	A,(_BUF)
	CP	0FEH
	JR	NZ,ERROR_FILE_NOT_OPEN

	LD	HL,BLOAD_RET
	LD	(SWC_BLOAD),HL

	LD	HL,(PARAM1)
	LD	A,(HL)
	CP	','
	JR	NZ,RBREAD_MEM
	INC	HL
	LD	A,(HL)
	CALL	CAP
	LD	(RUNBNF),A
	CP	'R'		;R,Sスイッチじゃ無い場合はオフセット値として処理する
	JR	Z,RBOS1
	CP	'S'
	JR	Z,RBOS1
	CP	','
	JR	NZ,RBOS2
RBOS1:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	Z,RBREAD_OSE
	CP	':'
	JR	Z,RBREAD_OSE
	CP	','		;次のパラメータを探す
	JR	NZ,RBOS1
RBOS2:				;オフセット
	LD	(PARAM1),HL
	LD	A,(HL)
	OR	A
	JR	Z,RBREAD_OSE
	LD	IX,FRMQNT
	CALL	CALSLT_BIOS
	LD	(PARAM1),HL
	LD	HL,(_BUF_ST)
	ADD	HL,DE
	LD	(_BUF_ST),HL
RBREAD_OSE:
	LD	A,(RUNBNF)
	CP	'S'
	JR	NZ,RBREAD_MEM
	CALL	WAIT_VDP
	LD	A,1
	LD	(FLG_LDIR),A
RBREAD_MEM:
	LD	HL,(_BUF_ST)
	LD	(SAVENT),HL
	LD	(_DTA),HL
	LD	HL,0FFFFH
	CALL	ROM_READ
	XOR	A
	LD	(FLG_LDIR),A
	LD	A,(RUNBNF)
	CP	'R'
	JR	NZ,RBREAD1
	LD	HL,(_BUF_EX)
	LD	(SWC_BLOAD),HL
RBREAD1:
ROM_NEXT:
	LD	HL,(PARAM1)
	LD	A,(HL)
	DEC	HL
	OR	A
	JR	Z,ROM_NEXT1
	CP	':'
	JR	Z,ROM_NEXT1
	INC	HL
ROM_NEXT1:
	LD	E,L
	LD	D,H

	CALL	SEARCH_END
	OR	A
	JR	Z,REM
RN3:					;マルチステートメントの処理
	LD	A,(HL)

	CP	0B5H			;LOAD
	JP	Z,ROM_LOAD
	CP	08AH			;RUN
	JR	Z,ROM_RUN
	CP	0B7H			;FILES
	JR	Z,ROM_FILES
	CP	0D6H			;COPY
	JP	Z,ROM_COPY
	CP	020H
	JR	Z,RN_SP

	LD	A,028H			;JR Z,
	LD	(SWC_BLOAD_M),A
	LD	A,(HL)
	RET
RN_SP:
	INC	HL
	JR	RN3

REM:
	EX	DE,HL
	LD	A,08FH			;REM
	RET

ROM_RUN:
	INC	HL
	LD	A,(HL)
	DEC	HL
	OR	A
	CALL	NZ,ROM_LOAD
	CP	08FH			;REM
	LD	A,08AH			;RUN
	RET	NZ
	LD	(HL),A
	RET

ROM_FILES:
	CALL	INIT_PARAM
	CALL	FILE_B
	CALL	GET_DISK_BANK_FDRV
	LD	A,(SECSZ_H)
	CALL	IS_BPB1
	JP	C,ERROR_DEVICE_IO_ERROR
	LD	A,(FNAME)
	CP	021H
	JR	NC,RFILES0
	LD	B,11
	CALL	FILE10
RFILES0:
	CALL	GET_DIR_DAT
RFILES1:
	CALL	FILESE
	JR	NC,RFILES_NOT_MATCH
RFILES_DISP:
	PUSH	HL
	LD	DE,0000BH	;属性
	ADD	HL,DE
#if exists USE_NORMAL32K_OR_ASCII16
	CALL	RDSLT_ROM
#else
	LD	A,(HL)
#endif
	POP	HL
	BIT	1,A		;不可視属性
	JR	NZ,RFILES_NEXT
	PUSH	HL
	LD	B,8
	CALL	MSN
#if exists USE_NORMAL32K_OR_ASCII16
	CALL	RDSLT_ROM
#else
	LD	A,(HL)
#endif
	CP	020H
	PUSH	AF
	LD	A,'.'
	CALL	NZ,MSG_A
	LD	B,3
	CALL	MSN
	POP	AF
	CALL	Z,MSG_A
	LD	A,(CSRX)
	LD	B,A
	LD	A,(LINLEN)
	SUB	B
	CP	12
	JR	NC,RFILES2
	LD	A,00DH		;改行
	CALL	MSG_A
	LD	A,00AH
	JR	RFILES3
RFILES2:
	LD	A,020H
RFILES3:
	CALL	MSG_A
	POP	HL
RFILES_NEXT:
	CALL	FNEXT
RFILES_NOT_MATCH:
	JR	NZ,RFILES1
	JP	ROM_NEXT

INIT_PARAM:
	LD	(PARAM0),HL
	LD	(PARAM1),HL
	EX	DE,HL
	LD	(RUNBNF),A
	LD	(VALTYP),A
	PUSH	HL
	LD	A,(_DVSW)
	CALL	GET_CD
	LD	(_CDX),HL
	POP	HL
	INC	DE
	CALL	SPCUT
	LD	A,(DE)
	OR	A
	RET	Z
	CP	':'
	RET	Z
	CP	'('
	RET	Z
	EX	DE,HL
	LD	IX,FRMEVL
	CALL	CALSLT_BIOS
	LD	(PARAM1),HL
	RET

ROM_COPY:
	CALL	INIT_PARAM
	LD	A,(VALTYP)
	CP	3		;string
	JP	NZ,ROM_ORG	;最初の引数が文字列ではない場合はオリジナルを実行する

	CALL	FILE_B
	CALL	ROM_OPEN
	JP	C,ERROR_FILE_NOT_FOUND

	LD	HL,_BUF_W
	LD	(_DTA),HL
	LD	HL,4			;ヘッダを4バイト読み込む
	CALL	ROM_READ

	XOR	A
	LD	(_BUF_LO),A		;PSET
	LD	(_BUF_O),A		;向き

	LD	HL,(PARAM1)
RCP_PARAM1:
	LD	A,(HL)
	INC	HL
	OR	A
	JP	Z,ROM_NEXT1
	CP	':'
	JP	Z,ROM_NEXT1
	CP	','
	JR	NZ,RCP_PARAM1_

	LD	IX,FRMQNT
	CALL	CALSLT_BIOS
	LD	A,E
	ADD	A,A
	ADD	A,A
	LD	(_BUF_O),A
	LD	A,(HL)
RCP_PARAM1_:
	CP	'('
	JR	NZ,RCP_PARAM1
	LD	IX,FRMQNT
	CALL	CALSLT_BIOS

	LD	(_BUF_X),DE

RCP_PARAM2:
	INC	HL
	LD	A,(HL)
	CP	020H
	JR	Z,RCP_PARAM2
	CP	','
	JR	Z,RCP_PARAM2

	LD	IX,FRMQNT
	CALL	CALSLT_BIOS

	LD	A,(ACPAGE)
	LD	D,A
	LD	(_BUF_Y),DE
RCP_PARAM3:
	INC	HL
	LD	A,(HL)
	CP	020H
	JR	Z,RCP_PARAM3
	CP	')'
	JR	Z,RCP_PARAM3
	CP	','
	JR	NZ,RCP_ST

	INC	HL
	LD	IX,FRMQNT
	CALL	CALSLT_BIOS

	LD	A,E
	LD	(_BUF_P),A

RCP_PARAM4:
	LD	A,(HL)
	INC	HL
	CP	020H
	JR	Z,RCP_PARAM4

	CP	','
	JR	NZ,RCP_ST

	LD	A,(HL)
	LD	B,4
	CP	0C3H		;PRESET
	JR	Z,RCP_LO
	DEC	B		;3
	SUB	0F8H		;XOR
	JR	Z,RCP_LO
	DEC	B		;2
	INC	A		;OR
	JR	Z,RCP_LO
	DEC	B		;1
	INC	A		;AND
	JR	Z,RCP_LO
	DEC	B		;0
				;PSET
RCP_LO:
	LD	A,B
	LD	(_BUF_LO),A
RCP_ST:
	LD	HL,(STREND)
	LD	(_DTA),HL
	EX	DE,HL
	LD	HL,-512
	ADD	HL,SP
	XOR	A
	SBC	HL,DE
	JR	NC,RCP0
	LD	HL,BUF
	LD	(_DTA),HL
	LD	L,A	;0
	LD	H,A	;0
RCP0:
	INC	H
	LD	(_BUF_EN),HL
RCP1:
	DI
	CALL	WAIT_VDP

	LD	A,(WRVDP)
	LD	C,A
	INC	C		;C := PORT#1's address(WR)
	LD	A,36
	OUT	(C),A
	LD	A,080H+17
	OUT	(C),A		;R#17 := 36

	INC	C
	INC	C		;C := PORT#3's address(WR)
	LD	HL,(_BUF_X)
	OUT	(C),L		;R#36 DX
	OUT	(C),H		;R#37
	LD	HL,(_BUF_Y)
	OUT	(C),L		;R#38 DY
	OUT	(C),H		;R#39
	LD	HL,(_BUF_W)
	OUT	(C),L		;R#40 NX
	OUT	(C),H		;R#41
	LD	HL,(_BUF_H)
	OUT	(C),L		;R#42 NY
	OUT	(C),H		;R#43

	LD	IX,(SCRMOD)
	LD	A,(_BUF_LO)	;LOGICAL OPERATION
	OR	A
	JR	NZ,USE_LMMC	;PSETじゃない場合はLMMC

	LD	A,IXL
	CP	8
	JR	Z,USE_HMMC8	;スクリーン8でPSET
	CP	6
	LD	HL,(_BUF_X)
	LD	A,(_BUF_W)
	JR	NZ,SCR57
	OR	L		;スクリーン6
	AND	3
	JR	NZ,USE_LMMC
SCR57:				;スクリーン5,7
	OR	L
	AND	1
	JR	NZ,USE_LMMC
USE_HMMC:
	LD	IXL,8
USE_HMMC8:
	LD	A,0F0H		;R#46 HMMC command
	LD	(_BUF_LO),A
USE_LMMC:
	LD	DE,0
	LD	B,-1
	CALL	GET_PIXEL
	OUT	(C),A		;R#44 first DATA
	LD	A,(_BUF_O)
	OUT	(C),A		;R#45 DIX and DIY

	LD	A,(_BUF_LO)	;LOGICAL OPERATION
	OR	0B0H		;R#46 LMMC command
	OUT	(C),A

	LD	IYL,C		;IYL := PORT#3's address(WR)
	DEC	C
	DEC	C		;C := PORT#1's address(WR)
	LD	A,080H+44
	OUT	(C),A
	LD	A,080H+17
	OUT	(C),A		;R#17 := 44

	LD	A,(RDVDP)
	INC	A
	LD	IYH,A		;IYX := PORT#1's address(RD)
   	LD	A,2
	OUT	(C),A
	LD	A,08FH
	OUT	(C),A
	LD	A,(_BUF_LO)	;LOGICAL OPERATION
	AND	040H
	JR	NZ,LOOP_HMMC
LOOP_COPY:
	CALL	GET_PIXEL
	EX	AF,AF'
	LD	C,IYH
LOOP_COPY1:
	IN	A,(C)
	RRCA			;RRCAではサインフラグは変化しない
	JR	NC,EXIT_COPY	;check CE bit
	JP	P,LOOP_COPY1	;check TR bit
	EX	AF,AF'
	LD	C,IYL		;IYL := PORT#3's address(WR)
	OUT	(C),A
	JR	LOOP_COPY

EXIT_COPY:
	CALL	GET_STATUS_0
	EI
	JP	ROM_NEXT

LOOP_HMMC:
	DI			;必要
	LD	C,IYL		;IYL := PORT#3's address(WR)
	LD	B,E
	LD	A,E
	OR	A
	JR	Z,LOOP_HMMC1
	OTIR
LOOP_HMMC1:
	INC	D
LOOP_HMMC2:
	DEC	D
	JR	Z,LOOP_HMMC3
	OTIR
	JP	LOOP_HMMC2
LOOP_HMMC3:
	IN	A,(C)
	RRCA
	JR	NC,EXIT_COPY	;check CE bit
	LD	HL,(_BUF_EN)
	CALL	ROM_READ
	EX	DE,HL
	LD	HL,(_DTA)
	LD	A,D
	OR	E
	JR	NZ,LOOP_HMMC
	JR	LOOP_COPY1

WAIT_VDP:
	LD	A,2
	CALL	GET_STATUS
	RRCA
	JR	C,WAIT_VDP
GET_STATUS_0:
	XOR	A
GET_STATUS:
	PUSH	BC
	LD	BC,(RDVDP)
	INC	C
	OUT	(C),A
	LD	A,08FH
	OUT	(C),A
	IN	A,(C)
	POP	BC
	RET

GET_PIXEL256:
	LD	A,D
	OR	E
	JR	NZ,GET_PIXEL1
	LD	HL,(_BUF_EN)
	CALL	ROM_READ
	EX	DE,HL
	LD	HL,(_DTA)
GET_PIXEL1:
	LD	A,(HL)
	INC	HL
	DEC	DE
	RET

GET_PIXEL:
	LD	A,IXL
	CP	8
	JR	Z,GET_PIXEL256
	INC	B
	CP	6
	JR	Z,GET_PIXEL4

	BIT	0,B
	JR	NZ,GET_PIXEL1

	LD	A,D
	OR	E
	JR	NZ,GET_PIXEL2

	LD	HL,(_BUF_EN)
	CALL	ROM_READ
	EX	DE,HL
	LD	HL,(_DTA)

GET_PIXEL2:
	LD	A,(HL)
	RRCA
	RRCA
	RRCA
	RRCA
	RET

GET_PIXEL3:
	LD	A,D
	OR	E
	JR	NZ,GET_PIXEL5

	LD	HL,(_BUF_EN)
	CALL	ROM_READ
	EX	DE,HL
	LD	HL,(_DTA)
GET_PIXEL5:
	LD	A,(HL)
	RRCA
	RRCA
	RRCA
	RRCA
	RRCA
	RRCA
	RET

GET_PIXEL4:
	LD	A,B
	AND	3	;+0
	JR	Z,GET_PIXEL3
	DEC	A	;+1
	JR	Z,GET_PIXEL2
	DEC	A	;+2
	LD	A,(HL)
	RET	NZ
	RRCA		;+3
	RRCA
	RET

ROM_READ:
	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUSH	IY
	LD	(LEFTX),HL
	LD	HL,(_DTA)
	LD	(DTAX),HL
	LD	HL,(LEFTX)

	CALL	RBX1
	JR	C,RBRERR1
	LD	A,C
	EX	DE,HL
	SBC	HL,DE		;CP 0HL,CDE
	ADD	HL,DE
	SBC	A,000H
	JR	C,RBR1
	EX	DE,HL
RBR1:
	SBC	A,A
	AND	1
	LD	(_RESULT),A
	LD	A,H
	OR	L
	JP	Z,RBREND0

	LD	(_LEFT),HL	;Read record byte
	LD	(LEFTX),HL

	CALL	RBX2
	JR	Z,RBRB
				;先頭の端数
	CALL	RBXA
	JP	C,RBRERR
	PUSH	BC
	CALL	ROM_LDIR
	LD	(DTAX),DE
	LD	HL,(LEFTX)
	POP	DE
	AND	A
	SBC	HL,DE
	LD	(LEFTX),HL
	JR	Z,RBREND

RBRB:
	CALL	RBXB
	JR	Z,RBRC
RBRB1:
	PUSH	BC
	PUSH	DE
	CALL	CLUST
	EX	DE,HL
	LD	BC,(CLSZ)		;1クラスタのサイズ
	CALL	ROM_LDIR
	EX	DE,HL
	POP	DE
	POP	BC
	CALL	GNCL
	JP	C,RBRERR
	DJNZ	RBRB1
RBRC:
	CALL	RBXC
	JR	Z,RBREND

	CALL	CLUST
	EX	DE,HL
	CALL	ROM_LDIR
RBREND:
	CALL	RBXEND
RBREND0:
	POP	IY
	POP	BC
	POP	DE
	POP	AF	;(HL)
	XOR	A
	LD	A,(_RESULT)
	RET

RBRERR1:
	LD	A,001H
RBRERR2:
	POP	IY
	POP	BC
	POP	DE
	POP	HL
	SCF
	LD	HL,0
	RET
RBRERR:
	LD	A,0FFH
	JR	RBRERR2

FILE_DD:
	POP	HL
	DB	03EH
	RET
	LD	(SWC_BLOAD_M),A
ROM_ORG:
	LD	HL,(PARAM0)
	LD	A,(HL)
	RET
FILE_B:
	LD	E,0
	LD	A,(VALTYP)
	CP	3		;string
	JP	NZ,FILED

	LD	IX,FRESTR
	CALL	CALSLT_BIOS
	LD	E,(HL)
	INC	HL
	LD	A,(HL)
	LD	IXL,A
	INC	HL
	LD	A,(HL)
	LD	IXH,A
	LD	A,01FH
	JR	FILE_BAS
FILE_BDOS:
	LD	A,020H
FILE_BAS:
	LD	(_CHKSP),A
	XOR	A
	LD	(FDRV),A
	LD	A,E
	CP	4
	JR	C,FILEB1
	LD	A,(IX+3)
	CP	':'
	JR	Z,FILE_DD
	LD	A,E
FILEB1:
	CP	2
	JR	C,DEVI1
	LD	A,(IX+1)
	CP	':'
	JR	NZ,DEVI1
	LD	A,(IX+0)		;DRIVE
	CALL	CAP
	SUB	'@'
	INC	IX
	INC	IX
	DEC	E
	DEC	E
	LD	(FDRV),A
	PUSH	AF
	CALL	GET_CD_CDRV
	LD	(_CDX),HL
	POP	HL
DEVI1:
	LD	A,(IX+0)
	SUB	05CH		;\
	JR	NZ,NOROOT
	LD	L,A
	LD	H,A
	LD	(_CDX),HL
	INC	IX
	DEC	E
NOROOT:

SETDIR:
	CALL	FILED
	LD	A,(IX+0)
	CP	05CH		;\
	RET	NZ
	INC	IX
	DEC	E
	LD	A,(FNAME)
	CP	020H		;. の処理
	JR	Z,SETDIR

	CALL	ROM_OPEN
	OR	A
	RET	NZ

#if exists USE_NORMAL32K_OR_ASCII16
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0000BH	;属性(アトリビュート)
	ADD	HL,DE
	CALL	RDSLT_ROM
	POP	DE
	BIT	4,A		;ディレクトリ
#else
	LD	IY,(DIRENT1)
	BIT	4,(IY+00BH)	;属性(アトリビュート) ディレクトリ
#endif
	RET	Z
	CALL	FILEI
#if exists USE_NORMAL32K_OR_ASCII16
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0001AH	;ファイルの先頭クラスタ
	ADD	HL,DE
	CALL	RDSLT_ROM
	INC	HL
	LD	E,A
	CALL	RDSLT_ROM
	LD	D,A
	EX	DE,HL
	POP	DE
#else
	LD	L,(IY+01AH)	;ファイルの先頭クラスタ
	LD	H,(IY+01BH)
#endif
	LD	(_CDX),HL
	JR	SETDIR

FILED:
	CALL	FILEI
	LD	HL,FNAME

	LD	B,8
FILE2:
	CALL	CCHKF
	RET	Z
	CP	'*'
	JR	Z,FILE9
	CP	'.'
	JR	Z,FILE4
	LD	(HL),A
	INC	HL
	DJNZ	FILE2

FILE3:
	CALL	CCHKF
	RET	Z
	CP	'.'
	JR	NZ,FILE3

FILE4:
	LD	HL,FNAME+8
	LD	B,3

FILE4L:
	CALL	CCHKF
	RET	Z
	CP	'.'
	JR	NZ,FILE12
	LD	(FNAME),A	;Parent directory(..)
	LD	(FNAME+1),A
	LD	A,020H
FILE12:
	CP	'*'
	JR	Z,FILE10
	LD	(HL),A
	INC	HL
	DJNZ	FILE4L
	RET

FILE9:				;名前の*処理、名前の残りを?で埋める
	CALL	FILE10
	JR	FILE3

FILE10:
	LD	A,'?'
	JR	FILL_MEMORY
FILEI:				;名前＋拡張子をスペースで初期化
	LD	A,020H
	LD	BC,11*256	;C=0にしておく
	LD	HL,FNAME
FILL_MEMORY:			;HLからBバイトAで埋める
	LD	(HL),A
	INC	HL
	DJNZ	FILL_MEMORY
	RET

CCHKF:
	LD	A,E
	OR	A
	RET	Z
	LD	A,(IX+0)
	CALL	CCHK2
	RET	Z
	JP	FKAN

CCHK2:
	INC	C
	DEC	C
	RET	NZ
CCHK3:				;ZF=1 で使えない文字
	CP	'+'
	RET	Z
	CP	','
	RET	Z
	CP	'/'
	RET	Z
	CP	':'
	RET	Z
	CP	';'
	RET	Z
	CP	'='
	RET	Z
	CP	05CH	;\
	RET	Z
	PUSH	HL
	LD	HL,(_CHKSP)
	CP	L
	POP	HL
	RET	NC
	CP	A		;Z=1
	RET

SS1:
	INC	DE
SPCUT:				;スペースをカット
	LD	A,(DE)
	CP	020H
	JR	Z,SS1
	RET

SCREOF1:
	INC	DE
SPCUTEX:			;スペース改行などをカット
	LD	A,(DE)
	CP	020H
	JR	Z,SCREOF1
	CP	00DH
	JR	Z,SCREOF1
	CP	00AH
	JR	Z,SCREOF1
	CP	01AH
	JR	Z,SCREOF1
	RET

GETNUM:
	LD	HL,0
GETNUM1:
	LD	A,(DE)
	INC	DE
	SUB	'0'
	RET	C
	CP	10
	RET	NC
	LD	C,L
	LD	B,H
	ADD	HL,HL	;*2
	ADD	HL,HL	;*4
	ADD	HL,BC	;*5
	ADD	HL,HL	;*10
	LD	C,A
	LD	B,0
	ADD	HL,BC
	JR	GETNUM1

SEARCH_END:
	LD	A,(HL)
SEARCH_END1:
	INC	HL
	OR	A
	RET	Z
	CP	':'
	JR	NZ,SEARCH_END
	LD	A,(HL)
	CP	':'
	JR	Z,SEARCH_END1
	RET

FKANC:
	BIT	0,C
	CALL	Z,CAP2
	JR	FKANX
FKAN:			;漢字チェック
	INC	IX
	DEC	E
FKANX:
	BIT	0,C
	RES	0,C
	RET	NZ
	CP	080H
	RET	C
	CP	0A0H
	JR	C,FKAN1
	CP	0E0H
	RET	C
FKAN1:
	INC	C
	AND	A
	RET

CAP:
	CP	'a'
	RET	C
	CP	'z'+1
	RET	NC
	SUB	020H
	RET
CAP2:
	CALL	CAP
CAP3:				;
	CP	5
	JR	Z,CAP4
	CP	021H
	RET
CAP4:
	LD	A,0E5H
	RET

ROM_OPEN:
	CALL	GET_DISK_BANK_FDRV

	CALL	GET_DIR_DAT
	PUSH	DE
	CALL	FILESE
	POP	DE
	JR	NC,ROM_OPEN_C
ROM_OPEN_OK:
	LD	(DIRENT1),HL
	PUSH	HL
	XOR	A
	LD	L,A
	LD	H,A
	LD	(RR_LOW),HL
	LD	(RR_HIGH),HL
	POP	HL
	RET

ROM_OPEN_C:			;#XXXX 形式のサブディスク
	LD	A,(FNAME)
	CP	'#'
	SCF
	RET	NZ
	PUSH	DE
	LD	HL,FDRV
	LD	DE,TMP_DIRENT
	LD	BC,1+8+3
	LDIR
	LD	B,32-(1+8+3)
	CALL	ZERO_MEMORY_DE
	LD	A,010H			;ディレクトリ属性
	LD	(TMP_DIRENT+0000BH),A	;属性(アトリビュート)
	LD	A,(FNAME+1)
	CALL	HEX
	JR	C,POPDE_SCF
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	A,(FNAME+2)
	CALL	HEX
	JR	C,POPDE_SCF
	OR	E
	LD	(TMP_DIRENT+0001BH),A		;ファイルの先頭クラスタ
	LD	A,(FNAME+3)
	CALL	HEX
	JR	C,POPDE_SCF
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A
	LD	A,(FNAME+4)
	CALL	HEX
	JR	C,POPDE_SCF
	OR	E
	LD	(TMP_DIRENT+0001AH),A		;ファイルの先頭クラスタ
	POP	DE
	LD	HL,TMP_DIRENT
	JR	ROM_OPEN_OK
POPDE_SCF:
	POP	DE
	SCF
	RET

FILESE:
FILESE0:
#if exists USE_NORMAL32K_OR_ASCII16
	CALL	RDSLT_ROM
#else
	LD	A,(HL)
#endif
	OR	A
	RET	Z		;END:ZF=1 CF=0
	CP	0E5H
	JR	Z,FILESE1
	LD	DE,FNAME
	PUSH	HL
	CALL	CPFILE
	CALL	Z,CPFILE2
	POP	HL
	SCF
	RET	Z		;!!!:(ZF=1) CF=1
FILESE1:
	CALL	FNEXT
	JR	NZ,FILESE0
	OR	0FFH		;ZF=0 CF=0
	RET

FNEXT:
	LD	DE,020H
	ADD	HL,DE
	LD	DE,(_CDX)
	LD	A,D
	OR	E
	JR	NZ,FNEXT_SUBDIR
	DEC	C
#if exists USE_NORMAL32K_OR_ASCII16
#else
CHECK_DIR_PAGE:			;ディレクトリが2ページ目に跨るかチェックする
	PUSH	AF		;※フラグを保存する必要あり
	BIT	7,H
	JR	Z,CHECK_DIR_PAGE1
	LD	A,H
	SUB	020H
	LD	H,A
	LD	A,(_DIR_BANK)
	INC	A
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	(_DIR_BANK),A
CHECK_DIR_PAGE1:
	POP	AF
#endif
	RET

FNEXT_SUBDIR:			;サブディレクトリ
	DEC	C
	RET	NZ

	LD	DE,(_CDX)
	CALL	GNCL
	EX	DE,HL
	LD	(_CDX),HL
	JP	GET_SUBDIR

CPFILE:
	PUSH	BC
	LD	BC,00B00H
CPSTR1:
	LD	A,(DE)
	CP	'?'		;Wild card
	JR	Z,CPSTR2
#if exists USE_NORMAL32K_OR_ASCII16
	CALL	RDSLT_ROM
#else
	LD	A,(HL)
#endif
	CALL	FKANC
	PUSH	HL
	LD	H,A
	LD	A,(DE)
	RLC	C
	CALL	FKANC
	RRC	C
	CP	H		;CP (HL),(DE)
	POP	HL
	JR	NZ,CPSTR3
CPSTR2:
	INC	DE
	INC	HL
	DJNZ	CPSTR1
CPSTR3:
	POP	BC
	RET

CPFILE2:
#if exists USE_NORMAL32K_OR_ASCII16
	CALL	RDSLT_ROM
#else
	LD	A,(HL)
#endif
	AND	008H	;~0F7H
	RET

RBX1:
	PUSH	HL		;Record byte

	LD	DE,(RR_LOW)	;CDE=Random record
	LD	A,(RR_HIGH+1)
	LD	C,A

	LD	A,(_DIR_BANK)
#if exists USE_NORMAL32K_OR_ASCII16
	LD	(ASCII16_BANK1_SEL),A		;ASCII-16K
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0001CH
	ADD	HL,DE
	CALL	RDSLT_ROM
	INC	HL
	LD	E,A
	CALL	RDSLT_ROM
	INC	HL
	LD	D,A
	CALL	RDSLT_ROM
	EX	DE,HL		;AHL=File size
	POP	DE
#else
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	IY,(DIRENT1)
	LD	L,(IY+01CH)	;AHL=File size
	LD	H,(IY+01DH)
	LD	A,(IY+01EH)
#endif

	AND	A
	SBC	HL,DE
	SBC	A,C
	POP	DE

	RET	C
	LD	C,A
	EX	DE,HL	;CDE=File byte	HL=Record byte
	OR	D
	OR	E
	RET	NZ
	SCF
	RET

RBX2:
	LD	BC,(RR_LOW+1)
	CALL	RWXR
	LD	A,(CLSZ_H)
	DEC	A
	PUSH	HL
	LD	HL,(RR_LOW)
	AND	H
	OR	L
	POP	HL
	RET

RBXA:
	PUSH	DE
	CALL	CLUST
	LD	(_DTPS),DE
	POP	DE
	CALL	GNCL
	RET	C
	LD	(_CLPS),DE

	LD	BC,(RR_LOW)
	LD	HL,(CLSZ)
	LD	A,H
	DEC	A
	AND	B
	LD	B,A
	SBC	HL,BC		;remaining cluster

	LD	DE,(LEFTX)
	SBC	HL,DE		;CP HL,DE
	ADD	HL,DE
	JR	C,RBXA1
	EX	DE,HL
RBXA1:
	LD	A,(_BANK)	;GNCLでバンクを切り替えたのでデータのあるバンク
#if exists USE_NORMAL32K_OR_ASCII16
	LD	(ASCII16_BANK1_SEL),A		;ASCII-16K
#else
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
#endif
	PUSH	HL
	LD	HL,(_DTPS)
	ADD	HL,BC
	LD	DE,(DTAX)
	POP	BC
	RET

RBXB:
	LD	HL,(DTAX)
	LD	DE,(_CLPS)
	LD	A,(LEFTX+1)
	LD	B,A
	LD	A,(CLSZ_H)	;1024=4 / 512=2 / 256=1
RBXB1:
	RRA		;->CF
	JR	C,RBXB2
	SRL	B	;B=B/2
	JR	RBXB1
RBXB2:
	LD	A,B
	OR	A
	RET

RBXC:
	LD	BC,(LEFTX)
	LD	A,(CLSZ_H)
	DEC	A
	AND	B
	LD	B,A
	OR	C
	RET

RBXEND:
	LD	DE,(_LEFT)
	LD	HL,(RR_LOW)
	LD	A,(RR_HIGH+1)
	ADD	HL,DE
	ADC	A,0
	LD	(RR_LOW),HL
	LD	(RR_HIGH+1),A
	EX	DE,HL		;HL=Read byte
	RET

RWXR:
	LD	A,(CLSZ_H)	;1024=4 / 512=2 / 256=1
L_RWX:
	RRA		;->CF
	JR	C,E_RWX
	SRL	B	;BC=BC/2
	RR	C	;
	JR	L_RWX
E_RWX:
	LD	A,(_DIR_BANK)
#if exists USE_NORMAL32K_OR_ASCII16
	LD	(ASCII16_BANK1_SEL),A		;ASCII-16K

	PUSH	HL
	LD	HL,(DIRENT1)
	LD	DE,0001AH	;ファイルの先頭クラスタ
	ADD	HL,DE
	CALL	RDSLT_ROM
	INC	HL
	LD	E,A
	CALL	RDSLT_ROM
	INC	HL
	LD	D,A
	POP	HL
#else
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	IY,(DIRENT1)
	LD	E,(IY+01AH)	;ファイルの先頭クラスタ
	LD	D,(IY+01BH)
#endif
	CALL	GET_DISK_BANK_FDRV
RWX1:
	LD	(_CLPS),DE
	LD	A,D
	OR	E
	SCF
	RET	Z

	LD	A,B
	OR	C
	RET	Z

	CALL	GNCL
	RET	C
	DEC	BC
	CALL	ENDCL
	JR	C,RWX1
	SCF
	RET

NCL3:
	CALL	GET_DISK_BANK_FDRV
	LD	L,E
	LD	H,D
	SRL	H
	RR	L
	RRA
	ADD	HL,DE
	LD	BC,BANK1_ADR
	ADD	HL,BC
	LD	BC,(SECSZ)
	ADD	HL,BC
	RLA
	RET

GNCL:
	LD	A,D
	OR	E
	SCF
	RET	Z
	PUSH	BC
	PUSH	HL
	CALL	NCL3
	JR	C,GNC1
	LD	E,(HL)
	INC	HL
	LD	A,(HL)
	AND	00FH
	LD	D,A
	POP	HL
	POP	BC
	RET
GNC1:
	LD	A,(HL)
	INC	HL
	LD	D,(HL)
	LD	B,4
GNC1L:
	SRL	D
	RRA
	DJNZ	GNC1L

	LD	E,A
	POP	HL
	POP	BC
	AND	A
	RET

CLUST:
	EX	DE,HL
CLUST_HL:
	DEC	HL
	DEC	HL
	PUSH	BC
	LD	A,(CLSZ_H)
	CALL	MUL_AHL

	CALL	GET_DIR_POS
	LD	C,A
	LD	B,0
	ADD	HL,BC

	LD	BC,(BANK1_ADR+17)	;BPB_RootEntCnt
	SRL	B
	RR	C			;/2
	SRL	B
	RR	C			;/4
	SRL	B
	RR	C			;/8
	ADD	HL,BC
#if exists USE_NORMAL32K_OR_ASCII16
	LD	A,L
	LD	BC,-00020H	;先頭の8KB部分
	ADD	HL,BC
	JR	NC,CLUST2
	LD	C,L		;C=読み出し元
	ADD	HL,HL	;64KB→32KB
	ADD	HL,HL	;32KB→16KB	バンク切り替えのサイズに合わせる
	CALL	GET_DISK_BANK_FDRV
	ADD	A,H
	LD	(ASCII16_BANK1_SEL),A	;ASCII-16K
	LD	(_BANK),A
	LD	L,C		;C=読み出し元
	LD	A,03FH		;セグメントのサイズでフィルタする(16K:3F)
	AND	L
	ADD	A,020H
#else
	LD	C,L		;C=読み出し元
	ADD	HL,HL	;64KB→32KB
	ADD	HL,HL	;32KB→16KB
	ADD	HL,HL	;16KB→8KB	バンク切り替えのサイズに合わせる
	CALL	GET_DISK_BANK_FDRV
	ADD	A,H
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	(_BANK),A
	LD	L,C		;C=読み出し元
	LD	A,01FH			;セグメントのサイズでフィルタする(8K:1F)
	AND	L
#endif
CLUST2:
	ADD	A,high BANK1_ADR
	LD	H,A
	LD	L,0
	EX	DE,HL
	POP	BC
	RET

ENDCL:
	LD	A,D	;END CLUSTER
	CP	00FH	;FAT12の最大値
	RET	NZ
	LD	A,E
	CP	0F7H
	RET

RB_READ:
	XOR	A	;HLA = HL*128
	SRL	H
	RR	L
	RRA
	LD	(RR_LOW),A
	LD	(RR_MID),HL
	LD	HL,128

	CALL	ROM_READ
	RET

GETSEQ:
	LD	L,(IY+32)
	LD	H,(IY+12)

	SLA	L

	SRL	H
	RR	L
	RET

SETSEQ:
	PUSH	AF
	LD	A,(RR_LOW)
	LD	HL,(RR_MID)

	CALL	SSQ1

	ADD	HL,HL
	SRL	L
	LD	(IY+32),L
	LD	(IY+12),H
	POP	AF
	RET

GETSIZE:
	LD	A,(IY+16)
	LD	L,(IY+17)
	LD	H,(IY+18)
SSQ1:
	ADD	A,A
	ADC	HL,HL
	OR	A
	RET	Z
	INC	HL
	RET

SET_FCB:
	PUSH	DE
	POP	IY
	LD	A,(IY+28)
	CP	0FFH
	JR	NZ,POPAF_SCF_FF_RET
	PUSH	HL
	LD	L,(IY+26)
	LD	H,(IY+27)
	LD	(_CLPS),HL
	LD	A,(IY+29)
	LD	(_DIR_BANK),A
	LD	L,(IY+30)
	LD	H,(IY+31)
	LD	(DIRENT1),HL
	POP	HL
	RET

POPAF_SCF_FF_RET:
	POP	AF
	SCF
	SBC	A,A
	RET

GET_DIR_DAT:
	LD	HL,(_CDX)
	LD	A,H
	OR	L
	JR	NZ,GET_SUBDIR
	CALL	GET_DIR_POS
	ADD	A,high BANK1_ADR
	LD	L,0
	LD	H,A			;HL=BANK1_ADR+(BPB_RsvdSecCnt+BPB_FATSz16*BPB_NumFATs)*BPB_BytsPerSec
#if exists USE_NORMAL32K_OR_ASCII16
#else
	CALL	CHECK_DIR_PAGE
#endif
	LD	A,(_BANK1)
	LD	(_DIR_BANK),A

	LD	A,(BANK1_ADR+17)	;BPB_RootEntCnt
	LD	C,A
	RET

GET_DIR_POS:				;ルートディレクトリ
	CALL	GET_DISK_BANK_FDRV
	LD	(_BANK1),A
	LD	A,(BANK1_ADR+16)	;BPB_NumFATs
	LD	B,A
	LD	A,(BANK1_ADR+22)	;BPB_FATSz16
	LD	C,A
	LD	A,(BANK1_ADR+14)	;BPB_RsvdSecCnt
GET_DIR_POS1:
	ADD	A,C
	DJNZ	GET_DIR_POS1

	LD	BC,(BANK1_ADR+11)	;BPB_BytsPerSec
	SCF		;無限ループ回避
L_MDP:
	RR	B	;->CF
	RET	C
	ADD	A,A
	JR	L_MDP

GET_SUBDIR:				;サブディレクトリ
	CALL	CLUST_HL
	EX	DE,HL
	LD	A,(_BANK)
	LD	(_DIR_BANK),A
	LD	A,(CLSZ_H)
	ADD	A,A		;*2
	ADD	A,A		;*4
	ADD	A,A		;*8
	LD	C,A
	RET

STATEMENT:
	LD	DE,STR_CHDIR
	CALL	CPPROCNM
	JR	Z,_CHDIR
	LD	DE,STR_CHDRV
	CALL	CPPROCNM
	JR	Z,_CHDRV
	LD	DE,STR_RAMDISK
	CALL	CPPROCNM
	JR	Z,_RAMDISK
	SCF
	RET
_CHDIR:
	LD	A,(HL)
	CP	'('
	SCF
	RET	NZ
	CALL	INIT_PARAM
	CALL	FILE_B
	CALL	ROM_CD
	RET	NC
	JP	ERROR_FILE_NOT_FOUND

_CHDRV:
	LD	A,(HL)
	CP	'('
	SCF
	RET	NZ
	CALL	INIT_PARAM
	PUSH	HL
	CALL	FILE_B
	POP	HL
	INC	HL
	LD	A,(FDRV)
	DEC	A
	JP	_SYS0EX1

_RAMDISK:
	LD	A,(HL)
	CP	'('
	SCF
	RET	NZ
	INC	HL
	LD	IX,FRMQNT
	CALL	CALSLT_BIOS
	PUSH	HL
	LD	DE,15		;切り上げの為
	ADD	HL,DE
	LD	A,L
	LD	B,4		;16で割る
RAMDISK1:
	SRL	H	;/2
	RRA
	DJNZ	RAMDISK1
	CP	0FFH
	JR	NZ,RAMDISK2
	DEC	A
RAMDISK2:
	LD	B,A
	CALL	_SYS68

	POP	HL
	INC	HL
	XOR	A
	RET

ROM_CD:
	LD	A,(FNAME)
	CP	020H
	JR	Z,CD1
	CALL	ROM_OPEN
	RET	C
#if exists USE_NORMAL32K_OR_ASCII16
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0000BH	;属性(アトリビュート)
	ADD	HL,DE
	CALL	RDSLT_ROM
	POP	DE
	BIT	4,A		;ディレクトリ
	JP	Z,ERROR_FILE_NOT_FOUND
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0001AH	;ファイルの先頭クラスタ
	ADD	HL,DE
	CALL	RDSLT_ROM
	INC	HL
	LD	E,A
	CALL	RDSLT_ROM
	LD	D,A
	EX	DE,HL
	POP	DE
#else
	LD	IY,(DIRENT1)
	BIT	4,(IY+00BH)	;属性(アトリビュート) ディレクトリ
	JP	Z,ERROR_FILE_NOT_FOUND
	LD	L,(IY+01AH)	;ファイルの先頭クラスタ
	LD	H,(IY+01BH)
#endif
CD2:
	CALL	SET_CD_FDRV
	LD	HL,(PARAM1)
CD_SKIP:
	LD	A,(HL)
	INC	HL
	CP	021H
	JR	C,CD_SKIP
	RET
CD1:
	LD	HL,(_CDX)
	JR	CD2

GET_BASIC_FCB:
	PUSH	DE
	INC	HL	;+1
	LD	E,(HL)	;FCB[1]
	INC	HL	;+2
	LD	D,(HL)	;FCB[2]
	INC	HL	;+3
	LD	(DIRENT1),DE
			;FCB[3]	backup char
	INC	HL	;+4
			;FCB[4]
	INC	HL	;+5
	LD	A,(HL)	;FCB[5]
	INC	HL	;+6
	LD	(_DIR_BANK),A
	LD	E,(HL)	;FCB[6]
	INC	HL	;+7
	LD	D,(HL)	;FCB[7]
	INC	HL	;+8
	LD	(RR_LOW),DE
	LD	A,(HL)	;FCB[8]
	INC	HL	;+9
	LD	(RR_HIGH),A
	LD	(_DTA),HL	;FCB[9]
	POP	DE
	RET

SET_BASIC_FCB:
	PUSH	HL
	LD	HL,(PTRFIL)
	INC	HL	;+1
	PUSH	DE
	LD	DE,(DIRENT1)
	LD	(HL),E	;FCB[1]
	INC	HL	;+2
	LD	(HL),D	;FCB[2]
	INC	HL	;+3
	LD	(HL),A	;FCB[3] backup char ハンドリング必要
	INC	HL	;+4
			;FCB[4] 変更しない
	INC	HL	;+5
	LD	A,(_DIR_BANK)
	LD	(HL),A	;FCB[5]
	INC	HL	;+6
	LD	DE,(RR_LOW)
	LD	(HL),E	;FCB[6]
	INC	HL	;+7
	LD	(HL),D	;FCB[7]
	INC	HL	;+8
	LD	A,(RR_HIGH)
	LD	(HL),A	;FCB[8]
	POP	DE
	POP	HL
	RET

;--------------------------------------
;FUNC_18:BACKUP
;in
;	C : backup charactor
;	HL: FBC pointer
;out
DEVICE_18_BACKUP:
	PUSH	DE
	PUSH	HL
	LD	DE,3
	ADD	HL,DE
	LD	(HL),C
	POP	HL
	POP	DE
	RET

DEVICE_CHECK:
	LD	A,(PROCNM+1)
	OR	A
	RET	Z
	LD	DE,STR_ROM
	CALL	CPPROCNM
	JR	Z,DEVICE_OK
	SCF
	RET
DEVICE_OK:
	XOR	A
	RET

;--------------------------------------
;FUNC_0:open
;in
;	A : 0
;	C : receive buffer size ?
;	E : open mode
;	HL: FCB pointer
;out
;	CF: 0=OK ,1=ERROR
;note:
;
; FCB
; +00: OPEN MODE
; +01: 対象ファイルのディレクトリエントリのアドレスL
; +02: 対象ファイルのディレクトリエントリのアドレスH
; +03: backup charactor buffer
; +04: device ID (FF=CAS,FE=LPT,FD=CRT,FC=?,other=ROM-SLOT)
; +05: 対象ディレクトリのバンク
; +06: ランダムレコードL
; +07: ランダムレコードM
; +08: ランダムレコードH
; +09: バッファ

DEVICE_0_OPEN:
	LD	A,E
	CP	2		;FOR OUTPUT
	JR	Z,OPEN2
	PUSH	DE
	push	hl
;
	LD	A,1		;ドライブA
	LD	(FDRV),A
	LD	HL,FILNAM
	LD	DE,FNAME
	LD	BC,8+3
	CALL	INIT_FILE1
	CALL	ROM_OPEN
	JP	C,ERROR_FILE_NOT_FOUND
	pop	hl
	POP	DE
OPEN2:
	ld	(PTRFIL),hl	;ストアしないとエラーになる
	ld	(hl),e	;FCB[0]	;open type : 合わせないとエラーになる
	XOR	A
	LD	(LEFTX),A
	CALL	SET_BASIC_FCB
	ld	a,e
	cp	8
	ccf
	ret

DEVICE_2_CLOSE:
	XOR	A
;	LD	(HL),A
	LD	L,A
	LD	H,A
	LD	(PTRFIL),HL
	RET

DEVICE_ENTRY:
	CP	8
	JR	Z,DEVICE_8_SIN
	INC	A
	JR	Z,DEVICE_CHECK:
	DEC	A
	JR	Z,DEVICE_0_OPEN
	CP	14
	JR	Z,DEVICE_14_EOF
	CP	4
	JR	Z,DEVICE_4_RANDOM
	CP	10
	JR	Z,DEVICE_10_LOC
	CP	12
	JR	Z,DEVICE_12_LOF
	CP	2
	JR	Z,DEVICE_18_BACKUP
	CP	2
	JR	Z,DEVICE_2_CLOSE
	CP	6
	JR	Z,DEVICE_6_SOUT
	SCF
	RET

DEVICE_6_SOUT:
	XOR	A
	RET

DEVICE_8_SIN:
	CALL	GET_BASIC_FCB
	LD	HL,1
	CALL	ROM_READ
	LD	A,H
	OR	L
	JR	Z,CCF_RET
	LD	HL,(_DTA)
	LD	A,(HL)
	PUSH	AF
	CALL	SET_BASIC_FCB
	POP	AF
	CP	00AH
	RET	Z	;ZF=separate
;	CP	01AH
;	JR	Z,SCF_RET
	SCF		;
CCF_RET:
	CCF		;ZF=0 CF=0にしたい
	RET

;--------------------------------------
;FUNC_4:RANDOM
;in
;	A : 4
;	unknown
;out
;	unknown
;note:
DEVICE_4_RANDOM:
	PUSH	DE
	CALL	GET_BASIC_FCB
	CALL	FRCINT2HL
	LD	(RR_LOW),HL
	XOR	A
	CALL	SET_BASIC_FCB
	POP	DE
	XOR	A
	RET

;FUNC_10:LOC
;in
;	A : 10
;out
;	DAC    : value of received bytes
;	VALTYP : DAC type
DEVICE_10_LOC:
	CALL	GET_BASIC_FCB
	LD	HL,(RR_LOW)
	JR	RETURN_TYPE8_AHL

;--------------------------------------
;FUNC_14:EOF
;in
;	A : 14
;out
;	DAC    : value of EOF value (EOF=-1)
;	VALTYP : DAC type
DEVICE_14_EOF:
	CALL	GET_BASIC_FCB
	CALL	RBX1
	JR	C,DEVICE_14_EOF1
	LD	HL,1
	CALL	ROM_READ
	LD	HL,(_DTA)
	LD	A,(HL)
	CP	01AH
	SCF
	JR	Z,DEVICE_14_EOF1
	CCF
DEVICE_14_EOF1:
	SBC	A,A
	LD	L,A
	LD	H,A
return_type2_hl:
	ld	(DACDAT),hl
	ld	a,2
	ld	(VALTYP),a
	AND	A
	ret

;--------------------------------------
;FUNC_16:FPOS
;in
;	unknown
;out
;	unknown
;note:
;	illegal function call

;--------------------------------------
;FUNC_12:LOF
;in
;	A : 12
;out
;	DAC    : value of LOC+RSIQLN
;	VALTYP : DAC type
DEVICE_12_LOF:
	CALL	GET_BASIC_FCB

	LD	A,(_DIR_BANK)
#if exists USE_NORMAL32K_OR_ASCII16
	LD	(ASCII16_BANK1_SEL),A		;ASCII-16K
	PUSH	DE
	LD	HL,(DIRENT1)
	LD	DE,0001CH
	ADD	HL,DE
	CALL	RDSLT_ROM
	INC	HL
	LD	E,A
	CALL	RDSLT_ROM
	INC	HL
	LD	D,A
	CALL	RDSLT_ROM
	EX	DE,HL		;AHL=File size
	POP	DE
#else
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	LD	IY,(DIRENT1)
	LD	L,(IY+01CH)	;AHL=File size
	LD	H,(IY+01DH)
	LD	A,(IY+01EH)
#endif
RETURN_TYPE8_AHL:
	OR	A
	JR	NZ,RT8AHL1
	BIT	7,H
	JR	Z,return_type2_hl
RT8AHL1:
	PUSH	HL
	ADD	HL,HL
	ADC	A,A
	LD	(DACDAT),A
	LD	A,0
	ADC	A,A
	LD	(DACDAT+1),A

	LD	A,2
	LD	(VALTYP),A
	LD	IX,FRCDBL
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	CALL	CAL_MP

	LD	HL,DBL32768
	LD	DE,ARG
	LD	BC,8
	LDIR

	LD	IX,DECMUL
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	CALL	CAL_MP

	LD	HL,DAC
	LD	DE,ARG
	LD	BC,8
	LDIR

	POP	HL
	LD	(DACDAT),HL

	LD	A,2
	LD	(VALTYP),A
	LD	IX,FRCDBL
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	CALL	CAL_MP

	LD	IX,DECADD
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	CALL	CAL_MP

	LD	A,8
	LD	(VALTYP),A
	RET

DBL32768:
	DB	045H,032H,076H,080H,0,0,0,0

CPPROCNM:
	PUSH	HL
	LD	HL,PROCNM
CPPROCNM1:
	LD	A,(DE)
	INC	DE
	CP	(HL)
	INC	HL
	JR	NZ,CPPROCNM2
	OR	A
	JR	NZ,CPPROCNM1
CPPROCNM2:
	POP	HL
	RET

ROM_MKI:
	CALL	FRCINT2HL
	LD	(BUF+3),HL
	LD	A,2		;返す文字列の長さ
	LD	(BUF),A
	LD	HL,BUF+3
	LD	(BUF+1),HL
	LD	HL,BUF
	LD	(DACDAT),HL
	LD	A,3		;文字列
	LD	(VALTYP),A
	AND	A
	RET

ROM_CVI:
	LD	IX,FRESTR
	CALL	CALSLT_BIOS
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	return_type2_hl

ROM_DSKF:
	CALL	FRCINT2HL
	LD	HL,0
	JP	return_type2_hl

;
;	DACの値を整数化してHLで返す
;
FRCINT2HL:
	PUSH	IX
	LD	IX,FRCINT
	LD	IY,(EXPTBL-1) 	;メインROMスロット
	CALL	CAL_MP
	POP	IX
	LD	HL,(DACDAT)
	RET

WILDCARD:
	DB	"???????????"

STR_CHDIR:
	DB	"CHDIR",0
STR_CHDRV:
	DB	"CHDRV",0
STR_RAMDISK:
	DB	"RAMDISK",0
STR_ROM:
	DB	"ROM",0
