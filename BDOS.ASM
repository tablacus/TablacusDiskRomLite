_ERROR:
	XOR	A
	LD	B,A
	RET

ROM_BDOS:
	PUSH	HL
	LD	A,C
	ADD	A,A
	JR	C,_ERROR
	LD	L,A
	LD	H,high STABLE
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	EX	(SP),HL
	LD	A,C
	RET

_PRINT:
PRINT:
	LD	A,E
	JR	MSG_A

_SYS01:		;(BDOS)コンソール入力
	CALL	_SYS07
MSG_A:
	CP	3
	JR	Z,MSG_BR
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHPUT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
MSG_BR:
	PUSH	AF
	LD	A,(CSRX)
	CP	2
	JR	C,MSGA2
	POP	AF
MSG_CR:
	PUSH	AF
	LD	A,00DH
	CALL	MSGA1
	LD	A,00AH
	CALL	MSGA1
	POP	AF
	RET

_SYS02:		;(BDOS)コンソール出力
	CALL	BREAK
	JR	PRINTX

_SYS06:		;(BDOS)コンソール直接入出力
	LD	A,E
	INC	A
	JP	Z,_INKEY
PRINTX:
	JP	_PRINT

_SYS08:		;(BDOS)エコーなしコンソール入力
	CALL	_SYS07
	CP	3
	CALL	Z,_BREAK
	CP	013H		;CTRL+S
	RET	NZ
PAUSE:
	CALL	KEYBC

_BREAK:
BREAK:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,BREAKX
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CALL	C,_BREAK
	POP	AF
	RET
KEYBC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,KILBUF
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

_SYS09:		;(BDOS)文字列出力
	PUSH	DE
_SX09:
	LD	A,(DE)
	INC	DE
	CP	'$'
	JR	Z,POPDE_RET
	CALL	MSG_A
	JR	_SX09
POPDE_RET:
	POP	DE
	RET

_SYS07:
FLGET:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	A,(IS_BIOS)
	OR	A
	JR	NZ,FLGET_G1

	CALL	GETVRAM
	PUSH	HL
	LD	IX,RDVRM
	CALL	CALSLT_R
	POP	HL
	PUSH	AF
	PUSH	HL
	LD	A,0FFH
	LD	IX,WRTVRM
	CALL	CALSLT_R
	POP	HL
FLGET_G1:
	PUSH	HL
	LD	IX,CHGET
	CALL	CALSLT_R
	LD	H,A
	EX	(SP),HL
	LD	A,(IS_BIOS)
	OR	A
	JR	NZ,FLGET_G2
	POP	BC
	POP	AF
	PUSH	BC
	LD	IX,WRTVRM
	CALL	CALSLT_R
FLGET_G2:
	POP	AF
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CP	3
	RET
;	RET	NZ
;	JP	CPM_BOOT

_INKEY:
INKEY:
	CALL	CPM_CONST
	RET	Z
	JR	FLGET

_SYS0A:		;(BDOS)文字列入力
	PUSH	BC
	PUSH	HL
	PUSH	DE

	LD	A,(IS_BIOS)
	OR	A
	JR	Z,SX0A_CBIOS
	LD	A,(CSRX)
	LD	E,A
	LD	D,0
	PUSH	DE
	PUSH	IX
	LD	IX,PLININ
	CALL	CALSLT_R
	POP	IX
	POP	DE
	LD	HL,BUF-1
	PUSH	AF
	ADD	HL,DE
	POP	AF
	EX	DE,HL
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SX0A_CBIOS:
	CALL	GETL
	LD	DE,KBUF
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SAX0:
	LD	B,(HL)
	INC	HL
SAX1:
	INC	HL
	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,SAX2
SAX4:
	LD	(HL),00DH
	JR	SAX3
SAX2:
	LD	(HL),A
	INC	C
	DJNZ	SAX1
SAX3:
	POP	DE
	LD	A,C
	INC	DE
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	LD	A,01EH
	CALL	MSG_A
	XOR	A
	RET
;			;C-BIOSはPLININが意図通りに動作しないので
GETL:
	PUSH	IX
	PUSH	IY

	LD	A,(SCRMOD)
	OR	A
	JR	Z,GETL0
	LD	IX,INITXT
	CALL	CALSLT_R
	LD	IX,SETTXT
	CALL	CALSLT_R
GETL0:
	LD	HL,(CSRY)
	LD	(FSTPOS),HL
GETL1:
	CALL	_SYS08
	CP	9
	JR	NZ,GETL1V
	LD	HL,KBUF
	CALL	MSX
	JR	GETL1
GETL1V:
	LD	E,A
	CP	8
	CALL	Z,CTRL02
	CP	020H
	CALL	NC,INSERT
	CALL	MSGA1

	LD	A,E
	CP	00DH
	JR	NZ,GETL1

	LD	DE,KBUF
	LD	A,(LINLEN)
	LD	B,A
	CALL	ZERO_MEMORY_DE

	LD	HL,(FSTPOS)
	LD	A,(CSRY)
	LD	L,A
	PUSH	HL
	CALL	LOC0
	LD	C,L
	LD	B,H
	POP	HL
	LD	A,(LINLEN)
	SUB	H
	DEC	A
	LD	E,A
	LD	HL,KBUF
GETL2:
	CALL	_SCRN
	INC	BC
	LD	(HL),A
	INC	HL
	DEC	E
	JR	NZ,GETL2
	CALL	MSG_CR

	POP	IY
	POP	IX
GETL3:
	DEC	HL
	LD	A,(HL)
	CP	021H
	RET	NC
	LD	(HL),0
	DEC	D
	JR	NZ,GETL3
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
CONSTX:
CPM_CONST:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHSNS
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET

_SYS2A:		;(BDOS)日付の獲得
	LD	C,11		;年/Year→HL
	CALL	RDCLK_BCD
	LD	L,A
	LD	H,0
	LD	A,(EXBRSA)
	OR	A
	JR	Z,SX2A1
	LD	DE,1980		;1980年～2079年
	ADD	HL,DE
SX2A1:
	LD	C,9		;月/Month→D
	CALL	RDCLK_BCD
	LD	D,A

	LD	C,7		;日/Day→E
	CALL	RDCLK_BCD
	LD	E,A

	LD	C,6		;曜日/Week→A
RDCLK:
	PUSH	IX
	LD	IX,REDCLK
WRCLK1:
	LD	A,(EXBRSA)
	OR	A
	JR	Z,RDCLK1	;MSX1
	PUSH	IY
	LD	IYH,A
	LD	A,B
	CALL	_CALSLT
	POP	IY
RDCLK1:
	POP	IX
	RET
WRCLK:
	PUSH	IX
	LD	IX,WRTCLK
	JR	WRCLK1

RDCLK_BCD:
	CALL	RDCLK		;1の位
	LD	B,A
	INC	C
	CALL	RDCLK		;10の位
	ADD	A,A		;*2
	LD	C,A
	ADD	A,A		;*4
	ADD	A,A		;*8
	ADD	A,C		;*10(8+2)
	ADD	A,B		;1の位
	RET

_SYS2C:		;(BDOS)時刻の獲得
	LD	BC,00115H		;12時間計/24時間計の設定を24時間計に
	CALL	WRCLK
	LD	C,4		;H=時/Hour
	CALL	RDCLK_BCD
	LD	H,A
	LD	C,2		;L=分/Minute
	CALL	RDCLK_BCD
	LD	L,A
	LD	C,0		;D=秒/Second
	CALL	RDCLK_BCD
	LD	D,A
	XOR	A		;E=1/100秒
	LD	E,A
	RET

POS:
	PUSH	AF
	LD	HL,(CSRY)
	LD	A,L
	LD	L,H
	LD	H,A
	INC	L
	INC	H
	POP	AF
	RET

LOC:
	PUSH	AF
	PUSH	HL
	LD	A,L
	LD	L,H
	LD	H,A
	DEC	L
	DEC	H
	PUSH	IX
	LD	IX,POSIT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	AF
	RET

_SCRN:
SCRN:
	PUSH	HL
	PUSH	IX

	LD	L,C
	LD	H,B
	LD	IX,RDVRM
	CALL	CALSLT_R

	POP	IX
	POP	HL
	RET

CTRL02:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	ADD	A,2
	LD	B,A	;カーソルより右の文字数
	LD	H,C	;LINLEN
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
C8X1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	DEC	HL
	LD	D,C
	DJNZ	C8X1
	POP	DE
	POP	AF
	RET

INSERT:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	INC	A
	LD	B,A	;カーソルより右の文字数
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
INS1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	INC	HL
	LD	D,C
	DJNZ	INS1
	POP	DE
	POP	AF
	RET

CONOUT1:
	LD	E,C
	JP	_PRINT

GETVRAM:
	LD	HL,(CSRY)
LOC0:
	DEC	L
	DEC	H
	LD	C,H	;CSRX-1
	LD	E,L	;CSRY-1
LOC1:
	LD	A,(LINLEN)
	LD	H,A
	LD	D,0
	LD	L,D	;0
	LD	B,8
LOC2:
	ADD	HL,HL
	JR	NC,LOC3
	ADD	HL,DE
LOC3:
	DJNZ	LOC2
	ADD	HL,BC
	RET

_SYS0C:		;(BDOS)バージョン番号の獲得
	LD	HL,00022H
	XOR	A
	LD	A,L
	RET

_SYS0D:		;(BDOS)ディスク・リセット
	LD	HL,00080H
	LD	(_DTA),HL
	RET

_SYS0E:		;(BDOS)カレントドライブの設定
	LD	A,E
_SYS0EX1:
	CP	8
	CCF
	RET	C
	LD	(_DVSW),A
	RET

_SYS0F:		;(BDOS)ファイルのオープン
	PUSH	DE
	POP	IY
	CALL	INIT_FILE
	CALL	ROM_OPEN
	JR	C,SCF_FF_RET
;				Open(Read)
	LD	(IY+28),0FFH
;				FILENAME
	PUSH	IY
	POP	DE
	INC	DE
	LD	BC,11
	LDIR
;				Attribute
	LD	A,(HL)
	LD	(IY+13),A
;				TIME
	LD	DE,11
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	(IY+22),A
	LD	A,(HL)
	INC	HL
	LD	(IY+23),A
;				DATE
	LD	A,(HL)
	INC	HL
	LD	(IY+20),A
	LD	A,(HL)
	INC	HL
	LD	(IY+21),A
;				First cluster
	LD	A,(HL)
	INC	HL
	LD	(IY+26),A
	LD	A,(HL)
	INC	HL
	LD	(IY+27),A
;				SIZE
	LD	A,(HL)
	INC	HL
	LD	(IY+16),A
	LD	A,(HL)
	INC	HL
	LD	(IY+17),A
	LD	A,(HL)
	INC	HL
	LD	(IY+18),A
	LD	A,(HL)
	LD	(IY+19),A
	LD	HL,(DIRENT1)
	LD	(IY+30),L
	LD	(IY+31),H
	LD	A,(_DIR_BANK)
	LD	(IY+29),A
	XOR	A
	LD	(IY+12),A
	RET

_SYS10:		;(BDOS)ファイルのクローズ
	XOR	A
	RET

S11X1:
	LD	(IY+25),C       ;RootEntCnt
SCF_FF_RET:
	SCF
	SBC	A,A
	RET

_SYS11:		;(BDOS)最初のファイルの検索
	LD	(_FCB),DE
	PUSH	DE
	POP	IY
	CALL	INIT_FILE
	LD	(IY+24),1
	CALL	GET_DIR_DAT
S12X1:
	CALL    FILESE
	INC	(IY+24)
	JR	NC,S11X1
	DEC	C
	LD	(IY+25),C       ;RootEntCnt
	BIT	4,(IY+13)
	JR	Z,S12X2
	PUSH	HL
	POP	IX
	LD	A,(IX+11)
	CP	00FH
	JR	Z,S11NEXT
S12X2:
	LD	BC,32
	LD	DE,(_DTA)
	LD	A,(IY+0)
	LD	(DE),A		;ドライブ番号
	INC	DE
	LDIR			;ディレクトリエントリ
	XOR	A
	RET
S11NEXT:
	CALL	FNEXT
	JR	NZ,S12X1
	SCF
	SBC	A,A
	RET

_SYS12:
	LD	IY,(_FCB)
	PUSH	IY
	POP	DE
	CALL	INIT_FILE
S12X3:
	CALL	GET_DIR_DAT
	LD	B,(IY+24)
S12X4:
	PUSH	BC
	CALL	FNEXT
	POP	BC
	JR	Z,S12X5
	LD	A,(HL)
	CP	0E5H
	JR	Z,S12X4
	DJNZ	S12X4
S12X5:
	LD	C,(IY+25)
	JR	S12X1

_SYS14:		;(BDOS)シーケンシャルな読み出し
	CALL	SET_FCB
	CALL	GETSEQ
	CALL	RB_READ
	PUSH	HL
	CALL	SETSEQ
	POP	HL
SREAD:
	ADD	A,001H
	RET	C

	LD	A,L
	SUB	001H
	SBC	A,A
	AND	3
	RRA
	RET

_SYS18:		;(BDOS)ログインベクトルの獲得
_SYS1D:		;(BDOS)書き込みが禁止されているディスクの調査
	LD	HL,(_LVECTOR)
	XOR	A
	LD	H,A
	RET

_SYS19:		;(BDOS)カレントドライブ番号の獲得
	LD	A,(_DVSW)
	AND	A
	RET

_SYS1A:		;(BDOS)DTAの設定
	LD	(_DTA),DE
	XOR	A
	RET

_SYS1B:		;(BDOS)ディスク情報の獲得
	LD	A,E
	SUB	1
	CALL	C,_SYS19
	LD	E,A
	CALL	IS_BPB
	JR	C,S1B_E
	PUSH	AF
	LD	HL,SYS1B_DPB
	LD	A,(BANK1_ADR+21)	;BPB_Media
	LD	B,A
	LD	C,A
	LD	(SYS1B_FAT),A
	LD	A,E
	CALL	GETDPB
	LD	IX,SYS1B_DPB
	LD	IY,SYS1B_FAT
	LD	BC,(SYS1B_DPB+1+1)	;SECSIZ
	LD	DE,(SYS1B_DPB+1+13)	;MAXCLUS
	DEC	DE
	DEC	DE
	LD	HL,0			;書き込み不可なので0を返す
	POP	AF
	RET
S1B_E:
	SBC	A,A
	RET

_SYS21:		;(BDOS)ランダムな読み出し
	CALL	SET_FCB

	LD	L,(IY+33)	;(FCB)Random record
	LD	H,(IY+34)

	CALL	RB_READ
	JR	SREAD

_SYS23:		;(BDOS)ファイル・サイズの獲得
	CALL	_SYS0F
	RET	C

	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSIZE
_S24X1:
	LD	(IY+33),L	;(FCB)Random record
	LD	(IY+34),H
	LD	(IY+35),0
;	POP	DE		;^^^^^^^^^^^^^^^^^^^^^^^^
;	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
	POP	DE		;

	XOR	A
	RET

_SYS24:		;(BDOS)ランダム・レコード・フィールドの設定
	PUSH	HL
	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSEQ
	JR	_S24X1		;^^^^^^^^^^^^^^^^^^^^^^^^

_SYS27:		;(BDOS)ランダムブロック読み込み
	CALL	SET_FCB
	PUSH	HL
	LD	A,(IY+0)
	LD	(FDRV),A
	LD	A,(IY+24)
	LD	(_BANK),A
	LD	L,(IY+33)
	LD	H,(IY+34)
	LD	(RR_LOW),HL
	LD	L,(IY+35)
	LD	H,(IY+36)
	LD	(RR_HIGH),HL
	POP	HL
	CALL	ROM_READ
	LD	DE,(RR_LOW)
	LD	(IY+33),E
	LD	(IY+34),D
	LD	DE,(RR_HIGH)
	LD	(IY+35),E
	LD	(IY+36),D
	LD	A,(_BANK)
	LD	(IY+24),A
	SBC	A,A
	RET	C
	LD	A,(_RESULT)
	RET

_SYS29:
	PUSH	HL
	INC	HL
	CALL	_SYS5C
	EX	(SP),HL
	LD	A,C
	CALL	LD_HL_A
	LD	BC,14
	ADD	HL,BC
	POP	BC
	INC	BC
	LD	A,C
	CALL	LD_HL_A
	INC	HL
	LD	A,B
	CALL	LD_HL_A
	XOR	A
	RET

_SYS2F:
	LD	B,H
	LD	A,L
	LD	HL,(_DTA)
	JP	DISKIO

_SYS59:		;(BDOS)カレントディレクトリの取得
	LD	A,B
	CALL	GET_CD_CDRV
	LD	A,H
	OR	L
	JR	Z,S59E
	LD	A,'#'
	LD	(DE),A
	INC	DE
	CALL	HEXHL
	XOR	A
S59E:
	LD	(DE),A
	RET

_SYS5A:		;(BDOS)カレントディレクトリの変更
	CALL	GET_PARAM_IX
	CALL	FILE_BDOS
	CALL	ROM_CD
	SBC	A,A
	RET

_SYS5B:		;(BDOS)パス名の解析(_PARSE)
	PUSH	DE
	CALL	GET_PARAM_IX
	CALL	FILE_BDOS
	POP	DE
	JR	S5B_1

_SYS5C:		;(BDOS)ファイル名の解析(_PFILE)
	CALL	SPCUT_SL
	PUSH	DE

	PUSH	HL
	XOR	A
	CALL	GET_CD_CDRV
	LD	(_CDX),HL

	CALL	GET_PARAM_IX
	CALL	FILE_BDOS
	POP	HL

	LD	B,11
	LD	DE,FNAME
S5C_1:
	LD	A,(DE)
	INC	DE
	CALL	LD_HL_A
	INC	HL
	DJNZ	S5C_1

	PUSH	IX
	POP	HL
	LD	DE,BUF
	AND	A
	SBC	HL,DE
	POP	DE
	ADD	HL,DE
	EX	DE,HL
S5B_1:
	LD	HL,(_CDX)
	LD	A,(FDRV)
	LD	C,A
	XOR	A
	RET

_SYS6F:
	LD	BC,0006FH
	LD	DE,08A91H		;Tablacus Disk ROM Lite 認識コード
	XOR	A
	RET

_SYS68:
	LD	HL,_LVECTOR
	SET	7,(HL)
	LD	A,B
	OR	A
	LD	A,0
	RET	NZ
	RES	7,(HL)
	RET

INIT_FILE:
	EX	DE,HL
	LD	DE,FDRV
	LD	BC,1+8+3
INIT_FILE1:
	LDIR
	CALL	GET_DISK_BANK_FDRV
#if exists USE_MORMAL32K_OR_ASCII16
	LD	(ASCII16_BANK1_SEL),A		;ASCII-16K
#else
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
#endif
	LD	L,(IY+14)
	LD	H,(IY+15)
	LD	A,H
	CP	010H
	JR	NC,INIT_FILE1X
	OR	L
	DEC	HL
	JR	NZ,INIT_FILE2
INIT_FILE1X:
	LD	A,(IY+0)
	CALL	GET_CD_CDRV
INIT_FILE2:
	LD	(_CDX),HL			;カレントディレクトリ
	RET

ZERO_MEMORY_DE:
	XOR	A
FILL_MEMORY_DE:
	LD	(DE),A
	INC	DE
	DJNZ	FILL_MEMORY_DE
	RET

SS1_SL:
	INC	DE
SPCUT_SL:				;スペースをカット
	CALL	LD_A_DE
	CP	020H
	JR	Z,SS1_SL
	RET

GET_PARAM_IX:
	LD	B,0
	LD	HL,BUF
	PUSH	HL
	CALL	SPCUT_SL
GPIX1:
	CALL	LD_A_DE
	INC	DE
	LD	(HL),A
	INC	HL
	OR	A
	JR	Z,GPIX2
	INC	B
	JR	NZ,GPIX1
	DEC	B
GPIX2:
	POP	IX
	LD	E,B
	RET

LD_A_DE:
	LD	A,(DE)
	BIT	7,D
	RET	NZ
	PUSH	BC
	PUSH	DE
	PUSH	HL
	EX	DE,HL

	LD	BC,RAMAD0
	LD	A,H
	RLCA
	RLCA
	AND	3
	ADD	A,C
	LD	C,A
	LD	A,(BC)

	CALL	_RDSLT
	POP	HL
	POP	DE
	POP	BC
	RET

LD_HL_A_1:
	LD	(HL),A
	RET
LD_HL_A:
	BIT	7,H
	JR	NZ,LD_HL_A_1
	PUSH	BC
	PUSH	DE
	PUSH	HL

	LD	E,A
	LD	BC,RAMAD0
	LD	A,H
	RLCA
	RLCA
	AND	3
	ADD	A,C
	LD	C,A
	LD	A,(BC)

	CALL	_WRSLT
	POP	HL
	POP	DE
	POP	BC
	RET

HEXHL:
	LD	A,H
	CALL	HEXHX
	LD	A,L
HEXHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	HEXA2
	POP	AF
HEXA2:
	CALL	ASC
	LD	(DE),A
	INC	DE
	RET

HEX:
	CALL	CAP
	SUB	'0'
	RET	C
	CP	10
	CCF
	RET	NC
	CP	16+1
	RET	C
	SUB	7
	CP	10H
	CCF
	RET

_SYS03	EQU	SCF_FF_RET	;(BDOS)外部入力
_SYS13	EQU	SCF_FF_RET	;(BDOS)ファイルの削除
_SYS15	EQU	SCF_FF_RET	;(BDOS)シーケンシャルな書き込み
_SYS16	EQU	SCF_FF_RET	;(BDOS)ファイルの作成
_SYS17	EQU	SCF_FF_RET	;(BDOS)ファイル名の変更
_SYS22	EQU	SCF_FF_RET	;(BDOS)ランダムな書き込み
_SYS26	EQU	SCF_FF_RET	;(BDOS)ランダムブロック書き込み
_SYS28	EQU	SCF_FF_RET	;(BDOS)ランダムな書き込み・その2

_SYS2B	EQU	SCF_FF_RET	;(BDOS)日付の設定
_SYS2D	EQU	SCF_FF_RET	;(BDOS)時刻の設定
_SYS39	EQU	SCF_FF_RET	;(BDOS)サブディレクトリの作成
_SYS3A	EQU	SCF_FF_RET	;(BDOS)サブディレクトリの削除

	ALIGN	256
STABLE:
;0
	DW	_ERROR,_SYS01,_SYS02,_SYS03
	DW	_ERROR,_ERROR,_SYS06,_SYS07
	DW	_SYS08,_SYS09,_SYS0A,_SYS0B
	DW	_SYS0C,_SYS0D,_SYS0E,_SYS0F
;1
	DW	_SYS10,_SYS11,_SYS12,_SYS13
	DW	_SYS14,_SYS15,_SYS16,_SYS17
	DW	_SYS18,_SYS19,_SYS1A,_SYS1B
	DW	_ERROR,_SYS1D,_ERROR,_ERROR
;2
	DW	_ERROR,_SYS21,_SYS22,_SYS23
	DW	_SYS24,_ERROR,_SYS26,_SYS27
	DW	_SYS28,_SYS29,_SYS2A,_SYS2B
	DW	_SYS2C,_SYS2D,_ERROR,_SYS2F
;3
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_SYS39,_SYS3A,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;4
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;5
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_SYS59,_SYS5A,_SYS5B
	DW	_SYS5C,_ERROR,_ERROR,_ERROR
;6
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_SYS68,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_SYS6F
;7
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR

