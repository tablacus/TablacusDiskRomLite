POP_HL_ERROR:
	POP     HL
_ERROR:
	LD	B,0
	AND	A
	RET

ROM_BDOS:
	PUSH	HL
	LD	A,C
	ADD	A,A
	JR	C,_ERROR
	LD	L,A
	LD	H,high STABLE
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	EX	(SP),HL
	LD	A,C
	RET

_PRINT:
PRINT:
	LD	A,E
	JR	MSG_A

_SYS01:		;(BDOS)コンソール入力
	CALL	_SYS07
MSG_A:
	CP	3
	JR	Z,MSG_BR
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHPUT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
MSG_BR:
	PUSH	AF
	LD	A,(CSRX)
	CP	2
	JR	C,MSGA2
	POP	AF
MSG_CR:
	PUSH	AF
	LD	A,00DH
	CALL	MSGA1
	LD	A,00AH
	CALL	MSGA1
	POP	AF
	RET

_SYS02:		;(BDOS)コンソール出力
	CALL	BREAK
	JR	PRINTX

_SYS06:		;(BDOS)コンソール直接入出力
	LD	A,E
	INC	A
	JP	Z,_INKEY
PRINTX:
	JP	_PRINT

_SYS08:		;(BDOS)エコーなしコンソール入力
	CALL	_SYS07
	CP	3
	CALL	Z,_BREAK
	CP	013H		;CTRL+S
	RET	NZ
PAUSE:
	CALL	KEYBC

_BREAK:
BREAK:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,BREAKX
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CALL	C,_BREAK
	POP	AF
	RET
KEYBC:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,KILBUF
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	POP	AF
	RET

_SYS07:
FLGET:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHGET
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	CP	3
	RET
;	RET	NZ
;	JP	CPM_BOOT

_INKEY:
INKEY:
	CALL	CPM_CONST
	RET	Z
	JR	FLGET

_SYS0A:		;(BDOS)文字列入力
	PUSH	BC
	PUSH	HL
	PUSH	DE
	CALL	GETL
	LD	DE,KBUF
	POP	HL
	PUSH	HL
	LD	C,0
	JR	NC,SAX0
	INC	HL
	INC	HL
	JR	SAX4
SAX0:
	LD	B,(HL)
	INC	HL
SAX1:
	INC	HL
	LD	A,(DE)
	INC	DE
	OR	A
	JR	NZ,SAX2
SAX4:
	LD	(HL),00DH
	JR	SAX3
SAX2:
	LD	(HL),A
	INC	C
	DJNZ	SAX1
SAX3:
	POP	DE
	LD	A,C
	INC	DE
	LD	(DE),A
	DEC	DE
	POP	HL
	POP	BC
	AND	A
	RET
GETL:
	PUSH	IX
	PUSH	IY

	LD	HL,(CSRY)
	LD	(FSTPOS),HL
GETL1:
	CALL	_SYS08
	CP	9
	JR	NZ,GETL1V
	LD	HL,KBUF
	CALL	MSX
	JR	GETL1
GETL1V:
	LD	E,A
	CP	8
	CALL	Z,CTRL02
	CP	020H
	CALL	NC,INSERT
	CALL	MSGA1

	LD	A,E
	CP	00DH
	JR	NZ,GETL1

	LD	DE,KBUF
	LD	A,(LINLEN)
	LD	B,A
	CALL	ZERO_MEMORY_DE

	LD	HL,(FSTPOS)
	LD	A,(CSRY)
	LD	L,A
	PUSH	HL
	CALL	LOC0
	LD	C,L
	LD	B,H
	POP	HL
	LD	A,(LINLEN)
	SUB	H
	DEC	A
	LD	E,A
	LD	HL,KBUF
GETL2:
	CALL	_SCRN
	INC	BC
	LD	(HL),A
	INC	HL
	DEC	E
	JR	NZ,GETL2
	CALL	MSG_CR

	POP	IY
	POP	IX
GETL3:
	DEC	HL
	LD	A,(HL)
	CP	021H
	RET	NC
	LD	(HL),0
	DEC	D
	JR	NZ,GETL3
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
CONSTX:
CPM_CONST:
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHSNS
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET

_SYS2A:		;(BDOS)日付の獲得
	XOR	A
	LD	D,A
	LD	E,A
	LD	H,A
	LD	L,A
	RET

_SYS2C:		;(BDOS)時刻の獲得
	XOR	A
	LD	D,A
	LD	E,A
	LD	H,A
	LD	L,A
	RET

POS:
	PUSH	AF
	LD	HL,(CSRY)
	LD	A,L
	LD	L,H
	LD	H,A
	INC	L
	INC	H
	POP	AF
	RET

LOC:
	PUSH	AF
	PUSH	HL
	LD	A,L
	LD	L,H
	LD	H,A
	DEC	L
	DEC	H
	PUSH	IX
	LD	IX,POSIT
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	AF
	RET

_SCRN:
SCRN:
	PUSH	HL
	PUSH	IX

	LD	L,C
	LD	H,B
	LD	IX,RDVRM
	CALL	CALSLT_R

	POP	IX
	POP	HL
	RET

CTRL02:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	ADD	A,2
	LD	B,A	;カーソルより右の文字数
	LD	H,C	;LINLEN
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
C8X1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	DEC	HL
	LD	D,C
	DJNZ	C8X1
	POP	DE
	POP	AF
	RET

INSERT:
	PUSH	AF
	PUSH	DE
	LD	HL,(CSRY)
	LD	A,(LINLEN)
	LD	C,A
	SUB	H	;CSRX
	INC	A
	LD	B,A	;カーソルより右の文字数
	PUSH	BC
	CALL	LOC0
	POP	BC

	LD	D,020H
INS1:
	LD	IX,RDVRM
	CALL	CALSLT_R
	LD	C,A
	LD	A,D
	LD	IX,WRTVRM
	CALL	CALSLT_R
	INC	HL
	LD	D,C
	DJNZ	INS1
	POP	DE
	POP	AF
	RET

CONOUT1:
	LD	E,C
	JP	_PRINT

GETVRAM:
	LD	HL,(CSRY)
LOC0:
	DEC	L
	DEC	H
	LD	C,H	;CSRX-1
	LD	E,L	;CSRY-1
LOC1:
	LD	A,(LINLEN)
	LD	H,A
	LD	D,0
	LD	L,D	;0
	LD	B,8
LOC2:
	ADD	HL,HL
	JR	NC,LOC3
	ADD	HL,DE
LOC3:
	DJNZ	LOC2
	ADD	HL,BC
	RET

_SYS0C:		;(BDOS)バージョン番号の獲得
	LD	HL,00022H
	XOR	A
	LD	A,L
	RET

_SYS0D:		;(BDOS)ディスク・リセット
	LD	HL,00080H
	LD	(_DTA),HL
	RET

_SYS0E:		;(BDOS)カレントドライブの設定
	LD	(_DVSW),A
	RET

_SYS0F:		;(BDOS)ファイルのオープン
	PUSH	DE
	POP	IY
	CALL	INIT_FILE
	CALL	ROM_OPEN
	JR	C,SCF_FF_RET
;				Open(Read)
	LD	(IY+28),0FFH
;				FILENAME
	PUSH	IY
	POP	DE
	INC	DE
	LD	BC,11
	LDIR
;				Attribute
	LD	A,(HL)
	LD	(IY+13),A
;				TIME
	LD	DE,11
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	(IY+22),A
	LD	A,(HL)
	INC	HL
	LD	(IY+23),A
;				DATE
	LD	A,(HL)
	INC	HL
	LD	(IY+20),A
	LD	A,(HL)
	INC	HL
	LD	(IY+21),A
;				First cluster
	LD	A,(HL)
	INC	HL
	LD	(IY+26),A
	LD	A,(HL)
	INC	HL
	LD	(IY+27),A
;				SIZE
	LD	A,(HL)
	INC	HL
	LD	(IY+16),A
	LD	A,(HL)
	INC	HL
	LD	(IY+17),A
	LD	A,(HL)
	INC	HL
	LD	(IY+18),A
	LD	A,(HL)
	LD	(IY+19),A
	XOR	A
	LD	(IY+12),A
	RET

_SYS10:		;(BDOS)ファイルのクローズ
	XOR	A
	RET

S11X1:
	LD	(IY+25),C       ;RootEntCnt
	LD	(IY+14),L
	LD	(IY+15),H
SCF_FF_RET:
	SCF
	SBC	A,A
	RET

_SYS11:		;(BDOS)最初のファイルの検索
	LD	(_FCB),DE
	PUSH	DE
	POP	IY
	CALL	INIT_FILE
	CALL	GET_DIR_DAT
S12X1:
	CALL    FILESE
	JR	NC,S11X1
	DEC	C
	LD	(IY+25),C       ;RootEntCnt
	LD	BC,32
	LD	DE,(_DTA)
	XOR	A
	LD	(DE),A		;ドライブ番号
	INC	DE
	LDIR			;ディレクトリエントリ
	LD	(IY+14),L
	LD	(IY+15),H
	RET

_SYS12:
	LD	IY,(_FCB)
	PUSH	IY
	POP	DE
	CALL	INIT_FILE

	LD	L,(IY+14)
	LD	H,(IY+15)
	LD	C,(IY+25)
	JR	S12X1

_SYS14:		;(BDOS)シーケンシャルな読み出し
	CALL	SET_FCB
	CALL	GETSEQ
	CALL	RB_READ
	PUSH	HL
	CALL	SETSEQ
	POP	HL
SREAD:
	ADD	A,001H
	RET	C

	LD	A,L
	SUB	001H
	SBC	A,A
	AND	3
	RRA
	RET

_SYS18:		;(BDOS)ログインベクトルの獲得
	LD	HL,00001H
	XOR	A
	RET

_SYS19:		;(BDOS)カレントドライブ番号の獲得
	LD	A,(_DVSW)
	RET

_SYS1A:		;(BDOS)DTAの設定
	LD	(_DTA),DE
	XOR	A
	RET

_SYS1B:		;(BDOS)ディスク情報の獲得
	LD	BC,512
	LD	DE,707
	LD	HL,0
	LD	IX,_BUF
	LD	IY,_BUF
	LD	(IY+0),0F9H
	LD	A,2
	RET

_SYS21:		;(BDOS)ランダムな読み出し
	CALL	SET_FCB

	LD	L,(IY+33)	;(FCB)Random record
	LD	H,(IY+34)

	CALL	RB_READ
	JR	SREAD

_SYS23:		;(BDOS)ファイル・サイズの獲得
	CALL	_SYS0F
	RET	C

	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSIZE
_S24X1:
	LD	(IY+33),L	;(FCB)Random record
	LD	(IY+34),H
	LD	(IY+35),0
;	POP	DE		;^^^^^^^^^^^^^^^^^^^^^^^^
;	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
	POP	DE		;

	XOR	A
	RET

_SYS24:		;(BDOS)ランダム・レコード・フィールドの設定
	PUSH	HL
	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSEQ
	JR	_S24X1		;^^^^^^^^^^^^^^^^^^^^^^^^

_SYS27:		;(BDOS)ランダムブロック読み込み
	CALL	SET_FCB
	PUSH	HL
	LD	L,(IY+33)
	LD	H,(IY+34)
	LD	(RR_LOW),HL
	LD	L,(IY+35)
	LD	H,(IY+36)
	LD	(RR_HIGH),HL
	POP	HL
	CALL	ROM_READ
	LD	DE,(RR_LOW)
	LD	(IY+33),E
	LD	(IY+34),D
	LD	DE,(RR_HIGH)
	LD	(IY+35),E
	LD	(IY+36),D
	SBC	A,A
	RET

_SYS2F:
	LD	B,H
	LD	HL,(_DTA)
	JP	DISKIO

INIT_FILE:
	EX	DE,HL
	LD	DE,FDRV
	LD	BC,1+8+3
	LDIR
	CALL	GET_DISK_BANK_FDRV
	LD	(BANK1_SEL),A			;ASCII-8K/Konami-8K
	LD	(KONAMI_SCC_BANK1_SEL),A	;Konami-8K/Konami-SCC
	RET

ZERO_MEMORY_DE:
	XOR	A
FILL_MEMORY_DE:
	LD	(DE),A
	INC	DE
	DJNZ	FILL_MEMORY_DE
	RET

	DS	05F00H-$
STABLE:
;0
	DW	_ERROR,_SYS01,_SYS02,_ERROR
	DW	_ERROR,_ERROR,_SYS06,_SYS07
	DW	_SYS08,_ERROR,_SYS0A,_SYS0B
	DW	_SYS0C,_SYS0D,_SYS0E,_SYS0F
;1
	DW	_SYS10,_SYS11,_SYS12,_ERROR
	DW	_SYS14,_ERROR,_ERROR,_ERROR
	DW	_SYS18,_SYS19,_SYS1A,_SYS1B
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;2
	DW	_ERROR,_ERROR,_ERROR,_SYS23
	DW	_SYS24,_ERROR,_ERROR,_SYS27
	DW	_ERROR,_ERROR,_SYS2A,_ERROR
	DW	_SYS2C,_ERROR,_ERROR,_SYS2F
;3
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;4
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;5
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;6
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;7
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR

