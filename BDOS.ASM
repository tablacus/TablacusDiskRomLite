POP_HL_ERROR:
	POP     HL
_ERROR:
	LD	B,0
	AND	A
	RET

ROM_BDOS:
	PUSH	HL
	LD	A,C
	ADD	A,A
	JR	C,_ERROR
	LD	L,A
	LD	H,high STABLE
	LD	A,(HL)
	INC	L
	LD	H,(HL)
	LD	L,A
	EX	(SP),HL
	LD	A,C
	RET

_SYS0B:		;(BDOS)コンソール状態のチェック
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	LD	IX,CHSNS
	CALL	CALSLT_R
	POP	IX
	POP	HL
	POP	DE
	POP	BC
	RET

_SYS0C:		;(BDOS)バージョン番号の獲得
	LD	HL,00022H
	XOR	A
	LD	A,L
	RET

_SYS0D:		;(BDOS)ディスク・リセット
	LD	HL,00080H
	LD	(_DTA),HL
	RET

_SYS0F:		;(BDOS)ファイルのオープン
	PUSH	DE
	POP	IY
	CALL	INIT_FILE

	CALL	ROM_OPEN_0F
	JR	C,SCF_FF_RET
;				Open(Read)
	LD	(IY+28),0FFH
;				FILENAME
	PUSH	IY
	POP	DE
	INC	DE
	LD	BC,11
	LDIR
;				Attribute
	LD	A,(HL)
	LD	(IY+13),A
;				TIME
	LD	DE,11
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	(IY+22),A
	LD	A,(HL)
	INC	HL
	LD	(IY+23),A
;				DATE
	LD	A,(HL)
	INC	HL
	LD	(IY+20),A
	LD	A,(HL)
	INC	HL
	LD	(IY+21),A
;				First cluster
	LD	A,(HL)
	INC	HL
	LD	(IY+26),A
	LD	A,(HL)
	INC	HL
	LD	(IY+27),A
;				SIZE
	LD	A,(HL)
	INC	HL
	LD	(IY+16),A
	LD	A,(HL)
	INC	HL
	LD	(IY+17),A
	LD	A,(HL)
	INC	HL
	LD	(IY+18),A
	LD	A,(HL)
	LD	(IY+19),A
	XOR	A
	LD	(IY+12),A
	RET

_SYS10:		;(BDOS)ファイルのクローズ
	XOR	A
	RET

S11X1:
	LD	(IY+25),C       ;RootEntCnt
	LD	(IY+14),L
	LD	(IY+15),H
SCF_FF_RET:
	SCF
	SBC	A,A
	RET

_SYS11:		;(BDOS)最初のファイルの検索
	LD	(_FCB),DE
	PUSH	DE
	POP	IY
	CALL	INIT_FILE

	LD	HL,_DIRBF	;ディレクトリの先頭
	LD	C,112		;RootEntCnt
S12X1:
	CALL    FILESE
	JR	NC,S11X1
	DEC	C
	LD	(IY+25),C       ;RootEntCnt
	LD	BC,32
	LD	DE,(_DTA)
	XOR	A
	LD	(DE),A		;ドライブ番号
	INC	DE
	LDIR			;ディレクトリエントリ
	LD	(IY+14),L
	LD	(IY+15),H
	RET

_SYS12:
	LD	IY,(_FCB)
	PUSH	IY
	POP	DE
	CALL	INIT_FILE

	LD	L,(IY+14)
	LD	H,(IY+15)
	LD	C,(IY+25)
	JR	S12X1

_SYS14:		;(BDOS)シーケンシャルな読み出し
	CALL	SET_FCB
	CALL	GETSEQ
	CALL	RB_READ
	PUSH	HL
	CALL	SETSEQ
	POP	HL
SREAD:
	ADD	A,001H
	RET	C

	LD	A,L
	SUB	001H
	SBC	A,A
	AND	3
	RRA
	RET

_SYS18:		;(BDOS)ログインベクトルの獲得
	LD	HL,00001H
	XOR	A
	RET

_SYS19:		;(BDOS)カレントドライブ番号の獲得
	XOR	A
	RET

_SYS1A:		;(BDOS)DTAの設定
	LD	(_DTA),DE
	XOR	A
	RET

_SYS1B:		;(BDOS)ディスク情報の獲得
	LD	BC,512
	LD	DE,707
	LD	HL,0
	LD	IX,_BUF
	LD	IY,_BUF
	LD	(IY+0),0F9H
	LD	A,2
	RET

_SYS21:		;(BDOS)ランダムな読み出し
	CALL	SET_FCB

	LD	L,(IY+33)	;(FCB)Random record
	LD	H,(IY+34)

	CALL	RB_READ
	JR	SREAD

_SYS23:		;(BDOS)ファイル・サイズの獲得
	CALL	_SYS0F
	RET	C

	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSIZE
_S24X1:
	LD	(IY+33),L	;(FCB)Random record
	LD	(IY+34),H
	LD	(IY+35),0
;	POP	DE		;^^^^^^^^^^^^^^^^^^^^^^^^
;	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
	POP	DE		;

	XOR	A
	RET

_SYS24:		;(BDOS)ランダム・レコード・フィールドの設定
	PUSH	HL
	PUSH	DE		;EX DE,IY
	EX	(SP),IY		;
;	POP	DE		;
;	PUSH	DE		;DEを破壊しないように注意vv
	CALL	GETSEQ
	JR	_S24X1		;^^^^^^^^^^^^^^^^^^^^^^^^

_SYS27:		;(BDOS)ランダムブロック読み込み
	CALL	SET_FCB
	PUSH	HL
	LD	L,(IY+33)
	LD	H,(IY+34)
	LD	(RR_LOW),HL
	LD	L,(IY+35)
	LD	H,(IY+36)
	LD	(RR_HIGH),HL
	POP	HL
	CALL	ROM_READ
	LD	DE,(RR_LOW)
	LD	(IY+33),E
	LD	(IY+34),D
	LD	DE,(RR_HIGH)
	LD	(IY+35),E
	LD	(IY+36),D
	SBC	A,A
	RET

_SYS2F:
	LD	B,H
	LD	HL,(_DTA)
	JP	DISKIO

INIT_FILE:
	EX	DE,HL
	LD	DE,FDRV
	LD	BC,1+8+3
	LDIR
	LD	A,DISK_BANK
	LD	(BANK1_SEL),A
	RET

	DS	05F00H-$
STABLE:
;0
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_SYS0B
	DW	_SYS0C,_SYS0D,_ERROR,_SYS0F
;1
	DW	_SYS10,_SYS11,_SYS12,_ERROR
	DW	_SYS14,_ERROR,_ERROR,_ERROR
	DW	_SYS18,_SYS19,_SYS1A,_SYS1B
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;2
	DW	_ERROR,_ERROR,_ERROR,_SYS23
	DW	_SYS24,_ERROR,_ERROR,_SYS27
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_SYS2F
;3
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;4
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;5
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;6
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
;7
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR
	DW	_ERROR,_ERROR,_ERROR,_ERROR

