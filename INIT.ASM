
;	Tablacus DISK ROM - INIT
;

INIT_ROM:
	LD	A,(DRVTBL)
	CP	1
	RET	Z

	LD	BC,-00080H
	LD	HL,(MEMSIZ)
	ADD	HL,BC
	LD	(MEMSIZ),HL
	LD	HL,(STKTOP)
	ADD	HL,BC
	LD	(STKTOP),HL
	LD	HL,0F080H
	LD	DE,0F000H
	LD	BC,00020H
	LDIR
	LD	HL,-00080H
	ADD	HL,SP
	LD	(BASSTK),HL
	LD	SP,(BASSTK)

	LD	A,0FFH
	LD	(DEVICE),A

	LD	A,1
	LD	(DRVTBL),A

	XOR	A
	LD	(_DVSW),A

	LD	HL,AT_ISC
	LD	DE,ISC
	LD	BC,ISC_E-ISC
	LDIR
	
	LD	A,0C3H		;JP
	LD	(H_GONE),A
	LD	(BDOS),A
	LD	(RAMUSE),A
	LD	(ROMUSE),A
	LD	HL,REPLACE_COMMAND
	LD	(H_GONE+1),HL
	LD	HL,H_BDOS
	LD	(BDOS+1),HL
	LD	HL,RAMUSE1
	LD	(RAMUSE+1),HL
	LD	HL,ROMUSE1
	LD	(ROMUSE+1),HL

	DB	03EH
	RST	30H
	LD	(H_BINS),A
	LD	(H_BINL),A
	LD	(H_FILE),A
	LD	(H_BDOS),A
	LD	(H_STKE),A
	LD	(H_PHYD),A

	CALL	GTSL1_
	LD	(ROM_SLT),A
	LD	(H_BINS+1),A
	LD	(H_BINL+1),A
	LD	(H_FILE+1),A
	LD	(H_BDOS+1),A
	LD	(H_STKE+1),A
	LD	(H_PHYD+1),A
	LD	(DRVTBL+1),A
	LD	(MASTERS),A

	LD	HL,ROM_LOAD
	LD	(H_BINS+2),HL
	LD	HL,ROM_RUN
	LD	(H_BINL+2),HL
	LD	HL,ROM_FILES
	LD	(H_FILE+2),HL
	LD	HL,ROM_BDOS
	LD	(H_BDOS+2),HL
	LD	HL,ROM_STKE
	LD	(H_STKE+2),HL
	LD	HL,DISKIO
	LD	(H_PHYD+2),HL

	DB	03EH
	RET
	LD	(H_FILE+4),A
	LD	(H_BINS+4),A
	LD	(H_BINL+4),A
	LD	(H_BDOS+4),A
	LD	(H_STKE+4),A
	LD	(H_PHYD+4),A

	XOR	A
	LD	(BANK1_SEL),A
	LD	A,(BANK1_ADR)
	CP	'A'
	JR	NZ,NOT_8K_BANK
	LD	A,DISK_BANK
	LD	(BANK1_SEL),A

	CALL	RSLREG		;read primary slot #
	RLCA
	RLCA
	PUSH	AF
	CALL	GTSL2_
	LD	(RAMAD3),A
	POP	AF	
	CALL	GTSL2_
	LD	(RAMAD2),A
RAMCHK2:
	LD	A,(RAMAD3)
	LD	H,040H
	CALL	CHK_RAM
	LD	(RAMAD1),A
RAMCHK1:
	LD	A,(RAMAD3)
	LD	H,00H
	CALL	CHK_RAM
	LD	(RAMAD0),A
RAMCHK0:
	LD	HL,(BANK1_ADR+19)	;BPB_TotSec16	
	LD	BC,0000FH		;切り上げ
	ADD	HL,BC
	LD	A,L

	LD	B,4
B_DRIVE1:
	SRL	H
	RRA
	DJNZ	B_DRIVE1

	ADD	A,DISK_BANK		;A=(TotSec16/16)+DISK_BANK
	LD	(B_DRIVE),A

#if exists FIX_DRIVE
	LD	A,(B_DRIVE)
	CP	FIX_DRIVE
	JR	Z,OK_FIX_DRIVE
	LD	HL,MSG_ERROR_DRIVE
	CALL	MSX
	LD	A,(B_DRIVE)
	CALL	PRTHX
	LD	IY,(EXPTBL) ;メインROMスロット
	LD	IX,CHGET
	CALL	_CALSLT
	LD	HL,MSG_CRLF
	CALL	MSX
OK_FIX_DRIVE:
#endif
	RET

NOT_8K_BANK:
				;ASCII-8K/Konami-8Kではない(ASCII-16K等)
	LD	HL,MSG_ERROR_ROM_MODE
	CALL	MSX
	LD	IY,(EXPTBL) ;メインROMスロット
	LD	IX,CHGET
	JP	_CALSLT

GTSL1_:				;自分のいるスロットを知る
	CALL	RSLREG		;read primary slot #
	RRCA
	RRCA
GTSL2_:
	AND	3		;[A]=000000PP
	LD	E,A		;[E]=000000PP
	LD	HL,EXPTBL
	ADD	A,L		;桁上りは無い
	LD	L,A		;[HL]=EXPTBL+000000PP
	LD	A,E		;[A]=000000PP
	BIT	7,(HL)
	RET	Z
	LD	A,L		;point to SLTTBL entry
	ADD	A,4		;桁上りは無い
	LD	L,A
	LD	A,(HL)		;get currently expansion slot register
	AND	00CH		;[A] = 0000SS00
	OR	E		;[A] = 0000SSPP
	OR	080H		;[A] = 1000SSPP
	RET
GTSL1_E:

CHK_RAM:
	PUSH	HL
	CALL	CHK_RAM0
	POP	HL
	RET	Z
	LD	A,(EXPTBL)		;スロット#0
	AND	080H
	CALL	CHK_RAM_SLT
	RET	Z
	LD	A,(EXPTBL+1)		;スロット#1
	AND	080H
	ADD	A,1
	CALL	CHK_RAM_SLT
	RET	Z
	LD	A,(EXPTBL+2)		;スロット#2
	AND	080H
	ADD	A,2
	CALL	CHK_RAM_SLT
	RET	Z
	LD	A,(EXPTBL+3)		;スロット#3
	AND	080H
	ADD	A,3
CHK_RAM_SLT:
	PUSH	HL
	CALL	CHK_RAM0		;スロット#X or X-0
	POP	HL
	RET	Z
	BIT	7,C
	JR	Z,CHK_RAM_E		;スロットは拡張されていない
	LD	A,C
	ADD	A,4			;スロット#X-1
	PUSH	HL
	CALL	CHK_RAM0
	POP	HL
	RET	Z
	LD	A,C
	ADD	A,4			;スロット#X-2
	PUSH	HL
	CALL	CHK_RAM0
	POP	HL
	RET	Z
	LD	A,C
	ADD	A,4			;スロット#X-3
	PUSH	HL
	CALL	CHK_RAM0
	POP	HL
CHK_RAM_E:
	XOR	A
	INC	A			;ZF=0にする
	LD	A,0
	RET

CHK_RAM0:
	LD	C,A
	LD	A,(ROM_SLT)
	CP	C
	JR	Z,CHK_RAM_E
	LD	L,0
CHK_RAM1:
	LD	A,C
	LD	IX,_RDSLT
	CALL	CALSLT_R
	ADD	A,0E5H
	LD	B,A
	LD	E,A
	LD	A,C
	LD	IX,_WRSLT
	CALL	CALSLT_R
	LD	A,C
	LD	IX,_RDSLT
	CALL	CALSLT_R
	CP	B
	RET	NZ
	SUB	0E5H
	LD	A,C
	LD	E,A
	LD	IX,_WRSLT
	CALL	CALSLT_R
	INC	H
	LD	A,L
	ADD	A,4
	LD	L,A
	JR	NZ,CHK_RAM1
	LD	A,C		;ZF=1のハズ
	RET

CALSLT_R:
	PUSH	BC
	PUSH	HL
	LD	IY,(EXPTBL) ;メインROMスロット
	CALL	_CALSLT
	POP	HL
	POP	BC
	RET

AT_ISC:
	ORG	ISC,AT_ISC-RUN
REPLACE_COMMAND:
	CP	0B7H			;FILES
	CALL	Z,H_FILE
	CP	0B5H			;LOAD
	JP	Z,H_BINS
	CP	08AH			;RUN
	JP	Z,H_BINL
	CP	0CFH			;BLOAD
	RET	NZ
FIX_BLOAD:
	RST	30H
ROM_SLT:
	DB	0
	DW	ROM_BLOAD
	PUSH	AF
	PUSH	HL
	CALL	BLOAD_RET
SWC_BLOAD	EQU	$-2
	POP	HL
	POP	AF
	CP	0CFH			;BLOAD
SWC_BLOAD_M:
	JR	Z,REPLACE_COMMAND
BLOAD_RET:
	RET
ROMUSE1:
	LD	A,(ROM_SLT)
	JR	ROMUSE2
RAMUSE1:
	LD	A,(RAMAD1)
ROMUSE2:
	LD	IX,_ENASLT
	LD	IY,(EXPTBL) 	;メインROMスロット
	JP	_CALSLT

_RESULT:
	DB	0
_BANK:
	DB	0
RR_LOW:
	DW	0
RR_MID	EQU	$-1
RR_HIGH:
	DW	0
_CLPS:
	DW	0
_LEFT:
	DW	0
_DTPS:
	DW	0
_DTA:
	DW	0
FLG_LDIR:
	DB	0
;	BDOS
	RST	30H
	DB	0
	DW	ROM_BDOS
	RET	
_FCB:
	DW	0
_BUF:
	DB	0
_BUF_ST:
	DW	0
_BUF_EN:
	DW	0
_BUF_EX:
	DW	0

DTAX:
	DW	0
B_DRIVE:
	DB	0
_DVSW:
	DB	0
FCBX:
	DW	0
LEFTX:
	DW	0
PARAM0:
	DW	0
PARAM1:
	DW	0
FDRV:
	DB	0
FNAME:
	DS	8+3

BASSTK	DW	0

ISC_E:
	ORG	$$+RUN			;$DEPHASE

MSX1:
	CALL	MSGA1
MSX:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	NZ,MSX1
	RET

PRTHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRTA2
	POP	AF
PRTA2:
	CALL	ASC
	JP	MSG_A
	
ASC:
	AND	$0F
	OR	$30
	CP	$3A
	RET	C
	ADD	A,7
	RET

MSN:
	LD	A,(HL)
	INC	HL
	CALL	MSG_A
	DJNZ	MSN
	RET

ROM_STKE:
	DB	03EH
	RET
	LD	(H_STKE),A
	PUSH	HL
	PUSH	DE
	PUSH	BC
	JR	BASAUTO

	XOR	A
	LD	E,A
	LD	D,A
	LD	B,1
	LD	HL,0C000H
	CALL	DISKIO

	LD	(BASSTK),SP
	LD	DE,RAMUSE
	XOR	A
	CALL	0C01EH
	LD	DE,RAMUSE
	SCF
	CALL	0C01EH
BASAUTO:
	LD	HL,AUTOEXEC
	LD	DE,FDRV
	LD	BC,1+8+3
	LDIR
	CALL	ROM_OPEN
	JR	C,RSTKEE
	LD	HL,RUN_AUTOEXEC
	LD	DE,KEYBUF
	LD	(GETPNT),DE
	LD	BC,RUN_AUTOEXEC_E-RUN_AUTOEXEC
	LDIR
	LD	(PUTPNT),DE
RSTKEE:
	POP	BC
	POP	DE
	POP	HL
	RET

MSG_ERROR_ROM_MODE:
	DB	"ASCII-8K/Konami-8K only!",13,10
NULL:
	DB	0

AUTOEXEC:
	DB	0,"AUTOEXECBAS"
RUN_AUTOEXEC:
	DB	'RUN "AUTOEXEC.BAS"',00DH
;	DB	'LOAD "AUTOEXEC.BAS"',00DH	;テスト用
RUN_AUTOEXEC_E:
