
;	Tablacus DISK ROM - INIT
;

INIT_ROM:
	LD	A,0FFH
	LD	(DEVICE),A

	LD	HL,AT_ISC
	LD	DE,ISC
	LD	BC,ISC_E-ISC
	LDIR

	LD	A,0C3H		;JP
	LD	(H_GONE),A
	LD	(BDOS),A
	LD	HL,ISC
	LD	(H_GONE+1),HL
	LD	HL,H_BDOS
	LD	(BDOS+1),HL

	DB	03EH
	RST	30H
	LD	(H_BINS),A
	LD	(H_BINL),A
	LD	(H_FILE),A
	LD	(H_BDOS),A
	LD	(H_STKE),A

	CALL	GTSL1_
	LD	(ROM_SLT),A
	LD	(H_BINS+1),A
	LD	(H_BINL+1),A
	LD	(H_FILE+1),A
	LD	(H_BDOS+1),A
	LD	(H_STKE+1),A

	LD	HL,ROM_LOAD
	LD	(H_BINS+2),HL
	LD	HL,ROM_BLOAD
	LD	(H_BINL+2),HL
	LD	HL,ROM_FILES
	LD	(H_FILE+2),HL
	LD	HL,ROM_BDOS
	LD	(H_BDOS+2),HL
	LD	HL,ROM_STKE
	LD	(H_STKE+2),HL

	DB	03EH
	RET
	LD	(H_FILE+4),A
	LD	(H_BINS+4),A
	LD	(H_BINL+4),A
	LD	(H_BDOS+4),A
	LD	(H_STKE+4),A

	XOR	A
	LD	(BANK1_SEL),A
	LD	A,(BANK1_ADR)
	CP	'A'
	JR	NZ,NOT_8K_BANK
	LD	A,DISK_BANK
	LD	(BANK1_SEL),A

	RET
NOT_8K_BANK:
				;ASCII-8K/Konami-8Kではない(ASCII-16K等)
	LD	HL,MSG_ERROR_ROM_MODE
	CALL	MSX
	LD	IY,(EXPTBL) ;メインROMスロット
	LD	IX,CHGET
	JP	_CALSLT

GTSL1_:				;自分のいるスロットを知る
	CALL	RSLREG		;read primary slot #
	RRCA
	RRCA
	AND	3		;[A]=000000PP
	LD	E,A		;[E]=000000PP
	LD	HL,EXPTBL
	ADD	A,L		;桁上りは無い
	LD	L,A		;[HL]=EXPTBL+000000PP
	LD	A,E		;[A]=000000PP
	BIT	7,(HL)
	RET	Z
	LD	A,L		;point to SLTTBL entry
	ADD	A,4		;桁上りは無い
	LD	L,A
	LD	A,(HL)		;get currently expansion slot register
	AND	00CH		;[A] = 0000SS00
	OR	E		;[A] = 0000SSPP
	OR	080H		;[A] = 1000SSPP
	RET

AT_ISC:
	ORG	ISC,AT_ISC-RUN
	CP	0B5H			;LOAD
	JP	Z,H_BINS
	CP	0CFH			;BLOAD
	JP	Z,H_BINL
	CP	0B7H			;FILES
	JP	Z,H_FILE
	CP	08AH			;RUN
	RET	NZ
FIX_RUN:
	RST	30H
ROM_SLT:
	DB	0
	DW	ROM_RUN
	RET
LOAD_LDIR:
	JP	ROM_LDIR
SWC_LDIR	EQU	$-2

ISC_E:
	ORG	$$+RUN			;$DEPHASE

MSX1:
	CALL	MSGA1
MSX:
	LD	A,(HL)
	INC	HL
	OR	A
	JR	NZ,MSX1
	RET

PRTHX:
	PUSH	AF
	RLCA
	RLCA
	RLCA
	RLCA
	CALL	PRTA2
	POP	AF
PRTA2:
	CALL	ASC
MSG_A:
MSGA1:
	PUSH	AF
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	IY,(EXPTBL) ;メインROMスロット
	LD	IX,CHPUT
	CALL	_CALSLT
	POP	HL
	POP	DE
	POP	BC
MSGA2:
	POP	AF
	RET
ASC:
	AND	$0F
	OR	$30
	CP	$3A
	RET	C
	ADD	A,7
	RET

MSN:
	LD	A,(HL)
	INC	HL
	CALL	MSG_A
	DJNZ	MSN
	RET

ROM_STKE:
	DB	03EH
	RET
	LD	(H_STKE),A
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	HL,AUTOEXEC
	LD	DE,FILNAM
	LD	BC,8+3
	LDIR
	CALL	ROM_OPEN_0F
	JR	C,RSTKEE
	LD	HL,RUN_AUTOEXEC
	LD	DE,KEYBUF
	LD	(GETPNT),DE
	LD	BC,RUN_AUTOEXEC_E-RUN_AUTOEXEC
	LDIR
	LD	(PUTPNT),DE
RSTKEE:
	POP	BC
	POP	DE
	POP	HL
	RET

MSG_ERROR_ROM_MODE:
	DB	"ASCII-8K/Konami-8K only!",13,10,0

AUTOEXEC:
	DB	"AUTOEXECBAS"
RUN_AUTOEXEC:
	DB	'RUN "AUTOEXEC.BAS"',00DH
RUN_AUTOEXEC_E:
